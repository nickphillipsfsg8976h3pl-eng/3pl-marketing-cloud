<table
    cellpadding="0"
    cellspacing="0"
    width="100%"
    role="presentation"
    style="min-width: 100%; "
    class="stylingblock-content-wrapper"
>
    <tr>
        <td class="stylingblock-content-wrapper camarker-inner">
            <!-- START OF PRODUCT STYLING SELECTION -->

            <!--
%%[
    VAR @acctId, @rows, @rowCount, @i, @row
    VAR @prod, @link, @allProds, @allLinks, @prodList, @singleLink
    VAR @allProdRS, @custProdRS, @missingList
    VAR @pRowset, @pCount, @p, @pName
    SET @acctId = AttributeValue("AccountId")
    SET @state = AttributeValue("Site State Province")
    IF EMPTY(@acctId) THEN
        SET @rowCount = 0
    ELSE
        SET @rows = LookupOrderedRows(
            "Renewal Opportunity - LookUp Table_Excluding3E_Email4",
            50,
            "Renewed Contract End Date ASC, OpportunityStage ASC",
            "AccountId",
            @acctId
        )
        SET @rowCount = RowCount(@rows)
    ENDIF
    /* Flag: Single vs Multiple opportunities */
    IF @rowCount == 1 THEN
        SET @isSingleOpp = "true"
    ELSEIF @rowCount > 1 THEN
        SET @isSingleOpp = "false"
    ENDIF
    SET @prodList = ""
    SET @allLinks = ""
    SET @singleLink = ""
    SET @linkHTML = ""
    /* Build renewal links */
    SET @allLinks = ""
    SET @linkCounter = 0
    /* Collect unique products */
    IF @rowCount > 0 THEN
        FOR @i = 1 TO @rowCount DO
            SET @row = Row(@rows, @i)
            SET @prod = Field(@row, "Productlist__c")
            SET @link = Field(@row, "Renewal_Form_Link__c")
            IF NOT EMPTY(@prod) THEN
                SET @pRowset = BuildRowsetFromString(@prod, ",")
                SET @pCount = RowCount(@pRowset)
                FOR @p = 1 TO @pCount DO
                    SET @pName = Trim(Field(Row(@pRowset, @p), 1))
                    /* Only add if not already in @prodList */
                    IF IndexOf(Lowercase(@prodList), Lowercase(@pName)) == 0 THEN
                        IF EMPTY(@prodList) THEN
                            SET @prodList = @pName
                        ELSE
                            SET @prodList = Concat(@prodList, ",", @pName)
                        ENDIF
                    ENDIF
                NEXT @p
            ENDIF
            /* Build renewal links */
            IF NOT EMPTY(@link) THEN
                IF @isSingleOpp == "true" THEN
                    SET @allLinks = Concat(
                        @allLinks,
                        '<a href="',
                        RedirectTo(@link),
                        '" style="color:#0a66c2; text-decoration:underline;">',
                        'here',
                        '</a>'
                    )
                ELSE
                    SET @allLinks = Concat(
                        @allLinks,
                        '<p style="font-family: Calibri, Arial, sans-serif; font-size: 11pt; margin: 0 0 11pt 0;">',
                        '<b>',
                        '<a href="',
                        RedirectTo(@link),
                        '" style="color:#0a66c2; text-decoration:underline;">Renew ',
                        @prod,
                        '</a>',
                        '<b>',
                        '</p>'
                    )
                ENDIF
            ENDIF
        NEXT @i
    ENDIF
    /* Format customer product list nicely */
    SET @allProds = ""
    SET @custProdRS = BuildRowsetFromString(@prodList, ",")
    SET @custCount = RowCount(@custProdRS)
    IF @custCount > 0 THEN
        FOR @i = 1 TO @custCount DO
            SET @pName = Trim(Field(Row(@custProdRS, @i), 1))
            IF @i == 1 THEN
                SET @allProds = @pName
            ELSEIF @i == @custCount THEN
                SET @allProds = Concat(@allProds, " and ", @pName)
            ELSE
                SET @allProds = Concat(@allProds, ", ", @pName)
            ENDIF
        NEXT @i
    ENDIF
    /* Master list of all products */
    SET @allProducts = "Reading Eggs,Mathseeds,Mathletics,Writing Legends"
    SET @allProdRS = BuildRowsetFromString(@allProducts, ",")
    /* Track missing list */
    SET @missingList = ""
    SET @missingCount = 0
    FOR @ap = 1 TO RowCount(@allProdRS) DO
        SET @apName = Trim(Field(Row(@allProdRS, @ap), 1))
        SET @found = 0
        FOR @cp = 1 TO @custCount DO
            IF Lowercase(Trim(Field(Row(@custProdRS, @cp), 1))) == Lowercase(@apName) THEN
                SET @found = 1
            ENDIF
        NEXT @cp
        IF @found == 0 THEN
            IF EMPTY(@missingList) THEN
                SET @missingList = @apName
            ELSE
                SET @missingList = Concat(@missingList, "||", @apName)
            ENDIF
            SET @missingCount = Add(@missingCount, 1)
        ENDIF
    NEXT @ap
    /* Format missing products list */
    SET @finalList = ""
    IF @missingCount > 0 THEN
        SET @missingRS = BuildRowsetFromString(@missingList, "||")
        IF @missingCount == 2 THEN
            /* Two → OR */
            SET @finalList = Concat(Field(Row(@missingRS, 1), 1), " or ", Field(Row(@missingRS, 2), 1))
        ELSE
            FOR @i = 1 TO @missingCount DO
                SET @mName = Field(Row(@missingRS, @i), 1)
                IF @i == 1 THEN
                    SET @finalList = @mName
                ELSEIF @i == @missingCount THEN
                    SET @finalList = Concat(@finalList, " or ", @mName)
                ELSE
                    SET @finalList = Concat(@finalList, ", ", @mName)
                ENDIF
            NEXT @i
        ENDIF
    ENDIF
    IF @rowCount == 1 THEN
        SET @SubjectLine = "Apologies – please use this updated renewal link"
    ELSE
        SET @SubjectLine = "Apologies – please use these updated renewal links"
    ENDIF
    VAR @content
    /* Example value from earlier script */
    SET @allProdsl = Lowercase(@allProds)
    /* normalize text */
    /* Initialize empty container */
    SET @content = ""
    /* Check each keyword and append block */
    IF IndexOf(@allProdsl, "reading") > 0 THEN
        SET @content = Concat(@content, ContentBlockById("612290"))
    ENDIF
    IF IndexOf(@allProdsl, "mathletics") > 0 AND @state != "Western Australia" THEN
        SET @content = Concat(@content, ContentBlockById("612283"))
    ENDIF
    IF IndexOf(@allProdsl, "mathletics") > 0 AND @state == "Western Australia" THEN
        SET @content = Concat(@content, ContentBlockById("612612"))
    ENDIF
    IF IndexOf(@allProdsl, "mathseeds") > 0 THEN
        SET @content = Concat(@content, ContentBlockById("612291"))
    ENDIF
    IF IndexOf(@allProdsl, "writing") > 0 THEN
        SET @content = Concat(@content, ContentBlockById("612293"))
    ENDIF
    /* Curriculum wording based on state */
    IF @state == "New South Wales" THEN
        SET @curriculum = "NSW Syllabus"
    ELSEIF @state == "Queensland" THEN
        SET @curriculum = "Australian Curriculum in Queensland"
    ELSEIF @state == "Victoria" THEN
        SET @curriculum = "Victorian Curriculum Version 2.0"
    ELSEIF @state == "Western Australia" THEN
        SET @curriculum = "Western Australian Curriculum"
    ELSE
        SET @curriculum = "Australian Curriculum, Version 9.0"
    ENDIF
    SET @TEMPLATE = "MX"
    IF @TEMPLATE == "MX" THEN
        set @BG_BRAND = "#FFFFFF"
        set @BG_BRAND_2 = "#FFFFFF"
        set @button_color = "#1981C4"
        set @H1_color = "#1981C4"
        set @H3_INLINE_color = "#1981C4"
        set @PRODUCT_LOGO = "https://image.my.3plearning.com/lib/fe3211717d640479701476/m/1/b5c88b13-45c4-4d4d-994d-9824e264d3c7.jpg"
        set @Logo_Height = "40"
        set @PRODUCT_ALT = "MATHLETICS LOGO"
        set @PRODUCT_URL = "https://www.mathletics.com/"
    ELSEIF @TEMPLATE == "3P" THEN
        set @BG_BRAND = "#FFFFFF"
        set @BG_BRAND_2 = "#FFFFFF"
        set @button_color = "#5FAD6E"
        set @H1_color = "#5FAD6E"
        SET @H2 = "#5A969C"
        set @H3_INLINE_color = "#4E5C67"
        set @PRODUCT_LOGO = "https://image.my.3plearning.com/lib/fe3211717d640479701476/m/1/4efa20c6-8ad4-448b-a37c-d1b457fe1210.gif"
        set @Logo_Height = "40"
        set @PRODUCT_ALT = "3P LOGO"
        set @PRODUCT_URL = "https://www.3plearning.com/"
    ELSEIF @TEMPLATE == "SS" THEN
        set @BG_BRAND = "#FFFFFF"
        set @BG_BRAND_2 = "#FFFFFF"
        set @button_color = "#FA4D16"
        set @H1_color = "#FA4D16"
        SET @H2 = "#4E5C67"
        set @H3_INLINE_color = "#FA4D16"
        set @PRODUCT_LOGO = "https://image.my.3plearning.com/lib/fe3211717d640479701476/m/1/9db1f18d-af65-4f68-9932-c248907b4070.png"
        set @Logo_Height = "40"
        set @PRODUCT_ALT = "3P LOGO"
        set @PRODUCT_URL = "https://www.3plearning.com/"
    ELSEIF @TEMPLATE == "WF" THEN
        set @BG_BRAND = "#FFFFFF"
        set @BG_BRAND_2 = "#FFFFFF"
        set @button_color = "#5C146A"
        set @H1_color = "#5C146A"
        SET @H2 = "#4E5C67"
        set @H3_INLINE_color = "#65006E"
        set @PRODUCT_LOGO = "https://image.my.3plearning.com/lib/fe3211717d640479701476/m/1/6692ca37-a73c-4914-9c0b-8c90fa158d1c.png"
        set @Logo_Height = "40"
        set @PRODUCT_ALT = "3P LOGO"
        set @PRODUCT_URL = "https://www.3plearning.com/"
    ELSEIF @TEMPLATE == "RE" THEN
        set @BG_BRAND = "#FFFFFF"
        set @BG_BRAND_2 = "#FFFFFF"
        set @button_color = "#23B0E6"
        set @H1_color = "#CA338C"
        SET @H2 = "#4E5C67"
        set @H3_INLINE_color = "#CA338C"
        set @PRODUCT_LOGO = "https://image.my.3plearning.com/lib/fe3211717d640479701476/m/1/172c30c2-a57c-4cf1-a717-45f900370824.png"
        set @Logo_Height = "40"
        set @PRODUCT_ALT = "3P LOGO"
        set @PRODUCT_URL = "https://www.3plearning.com/"
    ELSEIF @TEMPLATE == "MS" THEN
        set @BG_BRAND = "#FFFFFF"
        set @BG_BRAND_2 = "#FFFFFF"
        set @button_color = "#74457A"
        set @H1_color = "#74457A"
        SET @H2 = "#4E5C67"
        set @H3_INLINE_color = "#74457A"
        set @PRODUCT_LOGO = "https://image.my.3plearning.com/lib/fe3211717d640479701476/m/1/bc051573-cdb2-4a94-adf6-49282c1d23b9.png"
        set @Logo_Height = "40"
        set @PRODUCT_ALT = "3P LOGO"
        set @PRODUCT_URL = "https://www.3plearning.com/"
    ELSE
        set @BG_BRAND = "#FFFFFF"
        set @BG_BRAND_2 = "#FFFFFF"
        set @button_color = "#EF8800"
        set @H1_color = "#109E86"
        set @H3_INLINE_color = "#109E86"
        set @PRODUCT_LOGO = "https://image.my.3plearning.com/lib/fe3211717d640479701476/m/1/e27c8127-0e9e-4dfc-85b8-717eaba828d0.png"
        set @Logo_Height = "40"
        set @PRODUCT_ALT = "READIWRITER LOGO"
        set @PRODUCT_URL = "https://www.3plearning.com/software/readiwriter"
    ENDIF
]%%
-->
        </td>
    </tr>
</table>
