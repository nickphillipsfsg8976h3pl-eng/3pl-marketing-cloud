

%%[/*
****************************************************
-------- UPDATE SFDC OBJECT PREFERENCES ---------
****************************************************

Script functions

- Recieve form data from the preference centre
- Update SFDC Objects
- Respond with
  -- updated objects


  NB: this function will only update
  a  SFDC lead or contact object if the
  subscriber keys contain contain the 
  proper lead or contact Id (ie. begining
  in 00Q, or 003, respectiveley)


****************************************************
*/]%%





%%[



  /*///////////////////////////////
   GET REQUEST PARAMETERS
  ///////////////////////////////*/


  /*************
  Hidden Inputs
  **************/
  SET @SUBSCRIBER_KEY = RequestParameter("subscriber_key") 
  SET @JOB_ID = RequestParameter("job_id") 
  SET @EMAIL_NAME = RequestParameter("email_name") 
  SET @SUBSCRIBER_KEYS = BuildRowSetFromString(RequestParameter("subscriber_keys"), ",")
  SET @EMAIL_ADDRESS = RequestParameter("email_address")
  SET @PREVIOUS_BIRTHDATE = RequestParameter("previous_birthdate")
  SET @PREVIOUS_MOBILE_NUMBER = RequestParameter("previous_mobile_number")
  SET @PREVIOUS_PRODUCT_INFORMATION = IIF(Not Empty(RequestParameter("previous_product_information")), 'true', 'false')
  SET @PREVIOUS_NEWSLETTER = IIF(Not Empty(RequestParameter("previous_newsletter")), 'true', 'false')
  SET @PREVIOUS_PROMOTIONS = IIF(Not Empty(RequestParameter("previous_promotions")), 'true', 'false')
  SET @PREVIOUS_MATHLETICS = IIF(Not Empty(RequestParameter("previous_mathletics")), 'true', 'false')
  SET @PREVIOUS_MATHSEEDS = IIF(Not Empty(RequestParameter("previous_mathseeds")), 'true', 'false')
  SET @PREVIOUS_READING_EGGS = IIF(Not Empty(RequestParameter("previous_reading_eggs")), 'true', 'false')
  SET @PREVIOUS_BRIGHTPATH = IIF(Not Empty(RequestParameter("previous_brightpath")), 'true', 'false')
  SET @PREVIOUS_WRITINGLEGENDS = IIF(Not Empty(RequestParameter("previous_writinglegends")), 'true', 'false')
  SET @PREVIOUS_STATUS = IIF(Not Empty(RequestParameter("previous_status")), 'true', 'false')
  
  SET @hiddenInputs_json = Concat('{
  "SUBSCRIBER_KEY": "', @SUBSCRIBER_KEY, '",
  "SUBSCRIBER_KEYS": "', @SUBSCRIBER_KEYS, '",
  "EMAIL_ADDRESS": "', @EMAIL_ADDRESS, '",
  "PREVIOUS_BIRTHDATE": "', @PREVIOUS_BIRTHDATE, '",
  "PREVIOUS_MOBILE_NUMBER": "', @PREVIOUS_MOBILE_NUMBER, '",
  "PREVIOUS_PRODUCT_INFORMATION": "', @PREVIOUS_PRODUCT_INFORMATION, '",
  "PREVIOUS_NEWSLETTER": "', @PREVIOUS_NEWSLETTER, '",
  "PREVIOUS_PROMOTIONS": "', @PREVIOUS_PROMOTIONS, '",
  "PREVIOUS_MATHLETICS": "', @PREVIOUS_MATHLETICS, '",
  "PREVIOUS_MATHSEEDS": "', @PREVIOUS_MATHSEEDS, '",
  "PREVIOUS_READING_EGGS": "', @PREVIOUS_READING_EGGS, '",
  "PREVIOUS_BRIGHTPATH": "', @PREVIOUS_BRIGHTPATH, '",
  "PREVIOUS_WRITINGLEGENDS": "', @PREVIOUS_WRITINGLEGENDS, '",
  "PREVIOUS_STATUS": "', @PREVIOUS_STATUS, '"
  }')

  /***********
  Form Inputs
  ************/
  SET @NEXT_MOBILE_NUMBER = RequestParameter("next_mobile_number")
  SET @NEXT_BIRTHDATE = RequestParameter("next_birthdate")
  SET @NEXT_PRODUCT_INFORMATION = IIF(Not Empty(RequestParameter("next_product_information")), 'true', 'false')
  SET @NEXT_NEWSLETTER = IIF(Not Empty(RequestParameter("next_newsletter")), 'true', 'false')
  SET @NEXT_PROMOTIONS = IIF(Not Empty(RequestParameter("next_promotions")), 'true', 'false')
  SET @NEXT_MATHLETICS = IIF(Not Empty(RequestParameter("next_mathletics")), 'true', 'false')
  SET @NEXT_MATHSEEDS = IIF(Not Empty(RequestParameter("next_mathseeds")), 'true', 'false')
  SET @NEXT_READING_EGGS = IIF(Not Empty(RequestParameter("next_reading_eggs")), 'true', 'false')
  SET @NEXT_BRIGHTPATH_PROGRESS = IIF(Not Empty(RequestParameter("next_brightpath_progress")), 'true', 'false')
  SET @NEXT_WRITING_LEGENDS = IIF(Not Empty(RequestParameter("next_writing_legends")), 'true', 'false')
  SET @NEXT_STATUS = IIF(Not Empty(RequestParameter("next_status")), 'true', 'false')

  SET @formInputs_json = Concat('{
  "NEXT_MOBILE_NUMBER": "', @NEXT_MOBILE_NUMBER, '",
  "NEXT_BIRTHDATE": "', @NEXT_BIRTHDATE, '",
  "NEXT_PRODUCT_INFORMATION": "', @NEXT_PRODUCT_INFORMATION, '",
  "NEXT_NEWSLETTER": "', @NEXT_NEWSLETTER, '",
  "NEXT_PROMOTIONS": "', @NEXT_PROMOTIONS, '",
  "NEXT_MATHLETICS": "', @NEXT_MATHLETICS, '",
  "NEXT_MATHSEEDS": "', @NEXT_MATHSEEDS, '",
  "NEXT_READING_EGGS": "', @NEXT_READING_EGGS, '",
  "NEXT_BRIGHTPATH_PROGRESS": "', @NEXT_BRIGHTPATH_PROGRESS, '",
  "NEXT_WRITING_LEGENDS": "', @NEXT_WRITING_LEGENDS, '",
  "NEXT_STATUS": "', @NEXT_STATUS, '"
  }')

  /*///////////////////////////////
   CALCULATE PREFERENCES
  ///////////////////////////////*/



  SET @HAS_OPTED_OUT_OF_EMAIL = IIF(@NEXT_STATUS, 0, 1)

  SET @MARKETING_PRODUCT_INTEREST__C = Concat("",
   IIF(@NEXT_MATHLETICS, "Mathletics;", ""), 
   IIF(@NEXT_MATHSEEDS, "Mathseeds;", ""),
   IIF(@NEXT_READING_EGGS, "ReadingEggs;", ""),
   IIF(@NEXT_BRIGHTPATH_PROGRESS, "Brightpath;", ""),
   IIF(@NEXT_WRITING_LEGENDS, "WritingLegends;", ""),
  )/*concat*/

  IF Empty(@MARKETING_PRODUCT_INTEREST__C) THEN SET @MARKETING_PRODUCT_INTEREST__C = "None" ENDIF

  SET @MARKETING_INTEREST__C = Concat("",
   IIF(@NEXT_PRODUCT_INFORMATION, "Product Information;", ""),
   IIF(@NEXT_NEWSLETTER, "Newsletter;", ""),
   IIF(@NEXT_PROMOTIONS, "Promotions;", ""),
  )/*concat*/

  IF Empty(@MARKETING_INTEREST__C) THEN SET @MARKETING_INTEREST__C = "None" ENDIF



  /*//////////////////////////////////////
   UPDATE LEAD OR CONTACT SUBSCRIBER KEYS
  //////////////////////////////////////*/

  SET @TOTAL_RECORDS_UPDATED=0

  FOR @i=1 TO RowCount(@SUBSCRIBER_KEYS) DO

    SET @EACH_SUBSCRIBER_KEY = Field(Row(@SUBSCRIBER_KEYS, @i), 1)

    /*******************
     Contact or Lead?
    ********************/

    IF Substring(@EACH_SUBSCRIBER_KEY,1,3) == '00Q' THEN SET @SALESFORCE_OBJECT_TYPE = "Lead"
    ELSEIF Substring(@EACH_SUBSCRIBER_KEY,1,3) == '003' THEN SET @SALESFORCE_OBJECT_TYPE = "Contact" 
    ELSE VAR @SALESFORCE_OBJECT_TYPE ENDIF


    /*************************
     Update SFDC Object
    **************************/
    
    
      IF @SALESFORCE_OBJECT_TYPE == 'Lead' THEN
        SET @RECORD_UPDATED = UpdateSingleSalesforceObject(
            @SALESFORCE_OBJECT_TYPE, 
            @EACH_SUBSCRIBER_KEY, 
            'HasOptedOutOfEmail', @HAS_OPTED_OUT_OF_EMAIL, 
            'Marketing_Product_Interest__c', @MARKETING_PRODUCT_INTEREST__C, 
            'Marketing_Interest__c', @MARKETING_INTEREST__C
            ) 
      ENDIF


      IF @SALESFORCE_OBJECT_TYPE == 'Contact' THEN
        SET @RECORD_UPDATED = UpdateSingleSalesforceObject(
            @SALESFORCE_OBJECT_TYPE, 
            @EACH_SUBSCRIBER_KEY, 
            'HasOptedOutOfEmail', @HAS_OPTED_OUT_OF_EMAIL, 
            'Marketing_Product_Interest__c', @MARKETING_PRODUCT_INTEREST__C, 
            'Marketing_Interest__c', @MARKETING_INTEREST__C
            ) 
      ENDIF


    SET @TOTAL_RECORDS_UPDATED = @TOTAL_RECORDS_UPDATED + @RECORD_UPDATED

    set @logtime = SystemDateToLocalDate(now())
    insertData("Preference_Changes_Log","SubscriberKey", @EACH_SUBSCRIBER_KEY,"EmailAddress",
                @EMAIL_ADDRESS, "JobID", @JOB_ID, "LogTime",@logtime,"EmailName",@EMAIL_NAME,
                "hiddenInputs_json",@hiddenInputs_json,"formInputs_json",@formInputs_json,"HAS_OPTED_OUT_OF_EMAIL",
                @prevOptIns,"MARKETING_PRODUCT_INTEREST",@MARKETING_PRODUCT_INTEREST__C,
                "MARKETING_INTEREST",@MARKETING_INTEREST__C,"RECORD_UPDATED",@RECORD_UPDATED)

  NEXT @i





  /*///////////////////////////////
   RESPONSE
  ///////////////////////////////*/

  OutputLine(Concat('<b>SFDC Object Updated</b><br><br>'))



]%%

<br>
@MARKETING_PRODUCT_INTEREST__C: %%=V(@MARKETING_PRODUCT_INTEREST__C)=%%
<br>
@MARKETING_INTEREST__C: %%=V(@MARKETING_INTEREST__C)=%%
<br>
@TOTAL_RECORDS_UPDATED: %%=v(@TOTAL_RECORDS_UPDATED)=%%