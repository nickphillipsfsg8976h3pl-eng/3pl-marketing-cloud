%%[
SET @Debug = false /* Set to false to disable debug logs */
SET @DebugRawData = false 
SET @DoNotProcess = false /* Set to false to disable debug logs */
SET @DebugEmail = "" /* Leave blank to process all */

IF NOT EMPTY(@DebugEmail) THEN
  SET @rows = LookupRows("000_Lead_Submission_Queue_LAST_3DAYS", "Email", @DebugEmail)
ELSE
  SET @rows = LookupOrderedRows("000_Lead_Submission_Queue_LAST_3DAYS", 0, "SubmittedDate DESC", "IsProcessed", "false")
ENDIF

SET @recordCount = RowCount(@rows)

IF @recordCount > 0 THEN
  SET @rules = LookupOrderedRows("100_Lead_Validation_Rules_Config_LookUp", 0, "Priority ASC", "IsActive", "true")
  SET @ruleCount = RowCount(@rules)

  FOR @r = 1 TO @recordCount DO
    SET @row = Row(@rows, @r)
    SET @SubmissionId = Field(@row, "SubmissionId")
    SET @jsonStr = Field(@row, "SubmittedData")
    SET @parsed = BuildRowSetFromJSON(@jsonStr, "$.rows.[*]", 1)

    IF RowCount(@parsed) > 0 THEN
      SET @data = Row(@parsed, 1)
      SET @FirstName = IIF(NOT EMPTY(Field(@data, "FirstName")), Field(@data, "FirstName"), "unknown")
      SET @LastName  = IIF(NOT EMPTY(Field(@data, "LastName")), Field(@data, "LastName"), "unknown")
      SET @Email     = IIF(NOT EMPTY(Field(@data, "Email")), Field(@data, "Email"), "noemail@example.com")
      SET @Country   = IIF(NOT EMPTY(Field(@data, "Country")), Field(@data, "Country"), "unspecified")
      SET @JobTitle  = IIF(NOT EMPTY(Field(@data, "JobTitle")), Field(@data, "JobTitle"), "unspecified")

      SET @firstname_lc = Lowercase(@FirstName)
      SET @lastname_lc = Lowercase(@LastName)
      SET @email_lc = Lowercase(@Email)
      SET @jobtitle_lc = Lowercase(@JobTitle)
      SET @country_lc = Lowercase(@Country)

      SET @isBlocked = "false"
      SET @isAllowed = "false"
      SET @finalTargetDE = ""
      SET @finalErrorCode = ""
      SET @finalErrorMsg = ""

      IF @DebugRawData THEN
        output(concat(
          "<br><table border='1' cellpadding='6' cellspacing='0' style='border-collapse:collapse;'><tr>",
          "<th style='background:#ddd;'>#</th>",
          "<th style='background:#ddd;'>FirstName</th>",
          "<th style='background:#ddd;'>LastName</th>",
          "<th style='background:#ddd;'>Country</th>",
          "<th style='background:#ddd;'>Email</th>",
          "<th style='background:#ddd;'>JobTitle</th></tr><tr>",
          "<td>", @r, "</td>",
          "<td>", @FirstName, "</td>",
          "<td>", @LastName, "</td>",
          "<td>", @Country, "</td>",
          "<td>", @Email, "</td>",
          "<td>", @JobTitle, "</td></tr></table>"
        ))
      ENDIF

      IF @DoNotProcess THEN
      /*Do nothing */
      ELSE

        FOR @j = 1 TO @ruleCount DO
          IF @isBlocked != "true" THEN
            SET @rule = Row(@rules, @j)
            SET @action = Field(@rule, "Action")
            SET @type = Field(@rule, "RuleType")
            SET @targetField = Lowercase(Field(@rule, "TargetField"))
            SET @config = Field(@rule, "AdditionalConfig")
            SET @targetDE = Field(@rule, "TargetDE")
            SET @errorCode = Field(@rule, "RuleCode")
            SET @errorMsg = Field(@rule, "ErrorMessage")
            SET @match = "false"

            /*IF @Debug THEN
              output(concat("<br><strong>Rule #", @j, ":</strong>",
                "<br>&nbsp;&nbsp;RuleCode = ", @errorCode,
                "<br>&nbsp;&nbsp;Action = ", @action,
                "<br>&nbsp;&nbsp;RuleType = ", @type,
                "<br>&nbsp;&nbsp;TargetField = ", @targetField,
                "<br>&nbsp;&nbsp;Config = ", @config,
                "<br>&nbsp;&nbsp;TargetDE = ", @targetDE,
                "<br>&nbsp;&nbsp;ErrorMessage = ", @errorMsg))
            ENDIF*/

            IF @type == "Keyword" THEN
              SET @terms = BuildRowSetFromString(@config, ",")
              FOR @i = 1 TO RowCount(@terms) DO
                SET @term = Lowercase(Field(Row(@terms, @i), 1))
                IF (indexof(@targetField, "FirstName") >= 0 AND indexof(@firstname_lc, @term) > 0) OR
                   (indexof(@targetField, "LastName") >= 0 AND indexof(@lastname_lc, @term) > 0) OR
                   (indexof(@targetField, "JobTitle") >= 0 AND indexof(@jobtitle_lc, @term) > 0) OR
                   (indexof(@targetField, "Email") >= 0 AND indexof(@email_lc, @term) > 0) THEN
                  SET @match = "true"
                ENDIF
              NEXT @i
            ENDIF

            IF @type == "DomainMatch" THEN
              SET @terms = BuildRowSetFromString(@config, ",")
              FOR @i = 1 TO RowCount(@terms) DO
                SET @term = Lowercase(Field(Row(@terms, @i), 1))
                IF indexof(@email_lc, @term) > 0 THEN
                  SET @match = "true"
                ENDIF
              NEXT @i
            ENDIF
            
            IF @type == "EmailFormat" THEN
              IF NOT IsEmailAddress(@Email) THEN
                SET @match = "true"
              ENDIF
            ENDIF

            IF @type == "DELookup" THEN
              IF @errorCode == "PROFANITY_CHECK" THEN
                SET @isProfaneFirstName = RowCount(LookupRows(@config, "Name", @FirstName))
                SET @isProfaneLastName = RowCount(LookupRows(@config, "Name", @LastName))
                IF @isProfaneFirstName > 0 OR @isProfaneLastName > 0 THEN
                  SET @match = "true"
                ENDIF
              ELSE
                SET @parts = BuildRowSetFromString(@config, ":")
                IF RowCount(@parts) >= 3 THEN
                  SET @deName = Field(Row(@parts, 1), 1)
                  SET @lookupField = Field(Row(@parts, 2), 1)
                  SET @expectedValue = IIF(EMPTY(Field(Row(@parts, 3), 1)), "false", Lowercase(Field(Row(@parts, 3), 1)))
                  SET @lookupResult = Lookup(@deName, "CountryName", @lookupField, @Country)
                  SET @actualValue = IIF(EMPTY(@lookupResult), "false", Lowercase(@lookupResult))
                  IF @actualValue == @expectedValue THEN
                    SET @match = "true"
                  ENDIF
                ENDIF
              ENDIF
            ENDIF

            IF @match == "true" THEN
              UpsertDE(@targetDE, 1,
                "SubmissionId", @SubmissionId,
                "Email", @Email,
                "FirstName", @FirstName,
                "LastName", @LastName,
                "SubmittedData", @jsonStr,
                "IsProcessed", "true",
                "SubmittedDate", Now(),
                "ErrorCode", @errorCode,
                "ErrorDetails", @errorMsg)

              IF @action == "BLOCK" THEN
                SET @isBlocked = "true"
                SET @finalTargetDE = @targetDE
                SET @finalErrorCode = @errorCode
                SET @finalErrorMsg = @errorMsg
              ELSEIF @action == "ALLOW" THEN
                SET @isAllowed = "true"
                SET @finalTargetDE = @targetDE
                SET @finalErrorCode = @errorCode
                SET @finalErrorMsg = @errorMsg
              ENDIF
            ENDIF
          ENDIF
        NEXT @j

        IF @isBlocked == "true" THEN
          UpdateData("000_Lead_Submission_Queue_LAST_3DAYS", 1,
            "SubmissionId", @SubmissionId,
            "IsProcessed", "true",
            "ErrorCode", @finalErrorCode,
            "ErrorDetails", @finalErrorMsg)
          IF @Debug THEN
            output(concat("<br>",@r," <strong style='color:RED;'>BLOCKED</strong>: ",@FirstName,"|",@LastName,"|",@Country,"|",@Email,"|",@JobTitle, "--",@finalErrorCode))
          ENDIF
        ELSEIF @isAllowed == "false" THEN
          IF @Debug THEN
            output(concat("<br>",@r," <strong style='color:GREEN;'>PASSED</strong>: ",@FirstName,"|",@LastName,"|",@Country,"|",@Email,"|",@JobTitle))
          ENDIF
        ELSEIF @isAllowed == "true" THEN
          UpdateData("000_Lead_Submission_Queue_LAST_3DAYS", 1,
            "SubmissionId", @SubmissionId,
            "ErrorCode", @finalErrorCode,
            "ErrorDetails", @finalErrorMsg)
          IF @Debug THEN
            output(concat("<br>",@r," <strong style='color:ORANGE;'>ALLOWED</strong>: ",@FirstName,"|",@LastName,"|",@Country,"|",@Email,"|",@JobTitle, "--",@finalErrorCode))
          ENDIF
        ENDIF
      ENDIF

    ELSE
        IF @Debug THEN
          output(concat("<br>",@r," <strong style='color:RED;'>ERROR</strong>: ",@FirstName,"|",@LastName,"|",@Country,"|",@Email,"|",@JobTitle, "--",@finalErrorCode))
        ENDIF
    ENDIF
    
  NEXT @r
ENDIF
]%%
