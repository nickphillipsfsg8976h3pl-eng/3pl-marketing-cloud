
%%[ 

        /* HIDDEN FIELDS
        *****************/

        set @_Debug             = RequestParameter("debug")

        set @form               = lowercase(RequestParameter('form')) 
        Set @_Campaign_Name     = RequestParameter("campaign-name")
        Set @_Triggered_Send_Key= RequestParameter("triggered-send-key")
        Set @_Redirect_To_Page  = RequestParameter("redirect-to-page")
        set @RedirectId         = RequestParameter('rid') 
        set @pid                = RequestParameter('pid') 
        set @cid                = RequestParameter('cid') 
        set @_UTM_Campaign      = RequestParameter("utm-campaign")
        set @_UTM_Content       = RequestParameter("utm-content")
        set @_UTM_Medium        = RequestParameter("utm-medium")
        set @_UTM_Source        = RequestParameter("utm-source")
        set @_UTM_Term          = RequestParameter("utm-term")
        set @gclid              = RequestParameter("gclid")
        set @referrer           = RequestParameter("referrer")
        set @http_referrer      = HTTPRequestHeader("referer")


        set @_UTM_Campaign_entry = IIF(Not Empty(@_UTM_Campaign), @_UTM_Campaign, "NA")
        
        if Empty(@_UTM_Campaign) then
            if not Empty(@_Campaign_Name) then
                set @campaignNameLookup = Lookup("ENT.Campaign_Salesforce", "Name", "Id",@_Campaign_Name)
                if not Empty(@campaignNameLookup) then
                    set @_UTM_Campaign_entry = @campaignNameLookup
                endif
            endif
        endif

        /* FORM FIELDS
        *****************/

        set @_Product_Interest    = @pid
        set @_First_Name          = RequestParameter("first-name")
        set @_Last_Name           = RequestParameter("last-name")
        set @_Email_Address       = RequestParameter("email-address")
        set @_Phone_Number        = RequestParameter("phone-number")
        set @_Grade_Level         = RequestParameter("grade-level")
        set @_Job_Title           = RequestParameter("role_name")
        set @_Other_Job_Title     = RequestParameter("other_job_title")
        set @_re_interest         = RequestParameter("re_interest")
        set @_Country_Name        = RequestParameter("country-name")
        set @_User_Message        = RequestParameter("user_message")
        set @_Query_Type        = RequestParameter("query_type")
        set @_User_Type        = RequestParameter("user_type")
        set @_State_Name          = RequestParameter("state-name")
        set @_Postal_Code         = RequestParameter("postal-code")
        set @_School_Name         = RequestParameter("school_name_new")
        set @_Enquiry_Type         = RequestParameter("enquiry-type")
        set @_No_Of_Licences      = RequestParameter("no-of-licences-new")
        set @_Terms_And_Conditions= RequestParameter("terms-and-conditions")
        set @_Subscriber_Opt_In   = RequestParameter("subscriber-opt-in")
        

        /*************************************************************
        -- CREATE SALESFORCE LEAD OBJECT
        **************************************************************/

        set @LeadStatus = "MQL"
        set @LeadSource = "Marketing Cloud"
        set @LeadStage  = "Unassigned"
        
        if not empty(@_Job_Title) then
         if indexof(@_Job_Title, "Other")>0 then
                 set @_Job_Title = Concat(@_Job_Title," - ",@_Other_Job_Title)
         endif  
        endif
        
        /* Specifying State and Country Variables
        *****************/

        set @countrycode = Lookup("Country_DE","CountryCode","CountryName", @_Country_Name)
        set @rows        = LookupRows("state","State Code", @_State_Name,"Country Code", @countrycode)
        set @rowCount    = rowcount(@rows)
        set @stateName   =  @_State_Name
        if @rowCount > 0 then
            for @i = 1 to @rowCount do
                var @stateName
                set @row = row(@rows, @i) /* get row based on counter */
                set @stateName = field(@row,"State Name")
            next @i 
        endif
    
        
        if @form == 'quote' then 
            set @enquiry = 'Quote' 
        elseif @form == 'trial' then
            set @enquiry = 'Trial' 
        elseif @form == 'demo' then
            set @enquiry = 'Demo'
        elseif @form == 'info' then
            set @enquiry = 'Information'  
        else
            set @enquiry = ''
        endif


        if not empty(@_Product_Interest) then
           set @_Product_Interest = Replace(@_Product_Interest,",",";")
           set @_Product_Interest = Replace(@_Product_Interest,"brightpath","Brightpath Writing")
           set @_Product_Interest_c = @_Product_Interest
           if indexOf(@_Product_Interest,"mathspackage") > 0 then
             set @_Product_Interest = Replace(@_Product_Interest,",",";")
             set @_Product_Interest = Replace(@_Product_Interest,"mathspackage","")
             set @_Product_Interest = Replace(@_Product_Interest,";;",";")
             if indexOf(@_Product_Interest,"Mathletics") == 0 then
               set @_Product_Interest = concat(@_Product_Interest,";","Mathletics")
             endif
             if indexOf(@_Product_Interest,"Mathseeds") == 0 then
               set @_Product_Interest = concat(@_Product_Interest,";","Mathseeds")
             endif
             set @_Product_Interest_c = @_Product_Interest
             set @_Product_Interest_c = concat(@_Product_Interest_c,";","MathematicsPackage")
             set @mathpkg = 1
          endif
          if indexOf(@_Product_Interest,"literacypackage") > 0 then
             set @_Product_Interest = Replace(@_Product_Interest,",",";")
             set @_Product_Interest = Replace(@_Product_Interest,"literacypackage","")
             set @_Product_Interest = Replace(@_Product_Interest,";;",";")
             if indexOf(@_Product_Interest,"Reading") == 0 then
               set @_Product_Interest = concat(@_Product_Interest,";","ReadingEggs")
             endif
             if indexOf(@_Product_Interest,"Writing") == 0 then
               set @_Product_Interest = concat(@_Product_Interest,";","WritingLegends")
             endif
             if indexOf(@_Product_Interest,"Brightpath") == 0 then
               set @_Product_Interest = concat(@_Product_Interest,";","Brightpath Writing")
             endif
             set @_Product_Interest_c = @_Product_Interest
             set @_Product_Interest_c = concat(@_Product_Interest_c,";","LiteracyPackage")
           endif
           set @_Product_Interest_c = Replace(@_Product_Interest_c,"Writing Legends","WritingLegends")
           set @_Product_Interest = Replace(@_Product_Interest,"Writing Legends","WritingLegends")
           set @_Product_Interest_c = Replace(@_Product_Interest_c,"Brightpath Writing","Brightpath")
           if @mathpkg == 1 then set @_Product_Interest_c = concat(@_Product_Interest_c,";","MathematicsPackage") endif
           set @_Product_Interest_c = Replace(@_Product_Interest_c,";;",";")
           
        endif
        
        set @debug = false

        if @debug == true then
          OutputLine(Concat('<pre style="background-color:#ccc;"><code>@_User_Type: ', @_User_Type, '</code></pre>'))
          OutputLine(Concat('<pre style="background-color:#ccc;"><code>@_Query_Type: ', @_Query_Type, '</code></pre>'))
          OutputLine(Concat('<pre style="background-color:#ccc;"><code>@_User_Message : ', @_User_Message , '</code></pre>'))
          OutputLine(Concat('<pre style="background-color:#ccc;"><code>@_re_interest: ', @_re_interest, '</code></pre>'))
          OutputLine(Concat('<pre style="background-color:#ccc;"><code>@_Product_Interest: ', @_Product_Interest, '</code></pre>'))
          OutputLine(Concat('<pre style="background-color:#ccc;"><code>@_Product_Interest_c: ', @_Product_Interest_c, '</code></pre>'))
          OutputLine(Concat('<pre style="background-color:#ccc;"><code>@_First_Name: ', @_First_Name, '</code></pre>'))
          OutputLine(Concat('<pre style="background-color:#ccc;"><code>@_Last_Name: ', @_Last_Name, '</code></pre>'))
          OutputLine(Concat('<pre style="background-color:#ccc;"><code>@_Email_Address: ', @_Email_Address, '</code></pre>'))
          OutputLine(Concat('<pre style="background-color:#ccc;"><code>@_Phone_Number: ', @_Phone_Number, '</code></pre>'))
          OutputLine(Concat('<pre style="background-color:#ccc;"><code>@_Country_Name : ', @_Country_Name , '</code></pre>'))
          OutputLine(Concat('<pre style="background-color:#ccc;"><code>@stateName: ', @stateName, '</code></pre>'))
          OutputLine(Concat('<pre style="background-color:#ccc;"><code>@_Other_Job_Title: ', @_Other_Job_Title, '</code></pre>'))
          OutputLine(Concat('<pre style="background-color:#ccc;"><code>@_School_Name: ', @_School_Name, '</code></pre>'))
          OutputLine(Concat('<pre style="background-color:#ccc;"><code>@_Enquiry_Type : ', @_Enquiry_Type , '</code></pre>'))
          OutputLine(Concat('<pre style="background-color:#ccc;"><code>@enquiry : ', @enquiry , '</code></pre>'))
          OutputLine(Concat('<pre style="background-color:#ccc;"><code>@_Terms_And_Conditions: ', @_Terms_And_Conditions, '</code></pre>'))
          OutputLine(Concat('<pre style="background-color:#ccc;"><code>@_Subscriber_Opt_In : ', @_Subscriber_Opt_In, '</code></pre>'))
          OutputLine(Concat('<pre style="background-color:#ccc;"><code>@LeadStatus: ', @LeadStatus, '</code></pre>'))
          OutputLine(Concat('<pre style="background-color:#ccc;"><code>@LeadSource: ', @LeadSource, '</code></pre>'))
          OutputLine(Concat('<pre style="background-color:#ccc;"><code>@_Job_Title: ', @_Job_Title, '</code></pre>'))
          OutputLine(Concat('<pre style="background-color:#ccc;"><code>@LeadStage: ', @LeadStage, '</code></pre>'))
        else]%%

          <script runat="server">
           Platform.Load("core","1");
           try {
          </script>
%%[

            if @_Query_Type != 'help_subscription' and @_Query_Type != 'help_access_chg_details' then
              set @LEAD__New_Id = CreateSalesforceObject("Lead", 26,             
              "Marketing_Product_Interest__c", @_Product_Interest, 
              "FirstName", IIF(Not Empty(@_First_Name), @_First_Name, ""),  
              "LastName", IIF(Not Empty(@_Last_Name), @_Last_Name, ""),  
              "Email", IIF(Not Empty(@_Email_Address), @_Email_Address, "xxxx@xxxx.com"), 
              "Phone", IIF(Not Empty(@_Phone_Number), @_Phone_Number, 0000000000),  
              "Grade_Level__c", IIF(Not Empty(@_Grade_Level), @_Grade_Level, "Not Mentioned"),  
              "Title", IIF(Not Empty(@_Job_Title), @_Job_Title, 'Unknown'),  
              "Country", IIF(Not Empty(@_Country_Name), @_Country_Name, "Australia"),  
              "State__c", IIF(Not Empty(@stateName),@stateName, ""),   
              "State", IIF(Not Empty(@stateName),@stateName, ""),   
              "PostalCode", IIF(Not Empty(@_Postal_Code), @_Postal_Code, "0000"), 
              "Company", IIF(Not Empty(@_School_Name), @_School_Name, "NA Currently"),
              "No_of_licences__c",  IIF(Not Empty(@_No_Of_Licences), @_No_Of_Licences, 0), 
              "Marketing_Interest__c", IIF(@_Subscriber_Opt_In, "Promotions;Product Information;Newsletter", "Promotions;Product Information;"),
              "LeadSource", @LeadSource,
              "Status", @LeadStatus,
              "Enquiry_Type__c", @enquiry,
              'UTM_Source__c', IIF(Not Empty(@_UTM_Source), @_UTM_Source, @referrer),
              'UTM_Medium__c', IIF(Not Empty(@_UTM_Medium), @_UTM_Medium, 'Unknown'),
              'UTM_Campaign__c', IIF(Not Empty(@_UTM_Campaign), @_UTM_Campaign, 'Organic'),
              'UTM_Content__c', IIF(Not Empty(@_UTM_Content), @_UTM_Content, 'Unknown'),
              'UTM_Term__c', IIF(Not Empty(@_UTM_Term), @_UTM_Term, 'Unknown'),
              'Description', IIF(Not Empty(@Description), @Description, 'Unknown'),
              'GCLID__c', IIF(Not Empty(@gclid), @gclid, 'Unknown'),
              'Subject__c', IIF(Not Empty(@formattedSubjects), @formattedSubjects, ''),
              'Product_Interest__c',@_Product_Interest_c ) 
            endif

]%%
            <script runat="server">
             }
             catch (err) {
              Write("Error Message: " + Stringify(err.message) + Stringify(err.description));
             }
            </script>
                    
%%[

        
     
     
          if @_Query_Type == 'help_subscription' or @_Query_Type == 'help_access_chg_details' then
            if @_Query_Type == 'help_subscription'        then set @query ='I need help with my subscription' endif
            if @_Query_Type == 'help_access_chg_details'  then set @query ='Account access or change of details' endif
            if @_User_Type == 'parent_user'               then set @_User_Type ='I am a parent/homeschooler' endif
            if @_User_Type == 'school_user'               then set @_User_Type ='I am with a school' endif
            if @_User_Message == 'Any additional details you would like to add?'  then set @_User_Message ='' endif
            Var @jsonPayload

            set @jsonPayload = Concat('{"First Name": "', Replace(@_First_Name, '"', '\"'), '", ',
                                '"Last Name": "', Replace(@_Last_Name, '"', '\"'), '", ',
                                '"Email": "', Replace(@_Email_Address, '"', '\"'), '", ',
                                '"Phone Number": "', Replace(@_Phone_Number, '"', '\"'), '", ',
                                '"Country": "', Replace(@_Country_Name, '"', '\"'), '", ',
                                '"State": "', Replace(@stateName, '"', '\"'), '", ',
                                '"PinCode": "', Replace(@_Postal_Code, '"', '\"'), '", ',
                                '"User Type": "', Replace(@_User_Type, '"', '\"'), '", ',
                                '"Product Interest": "', Replace(@_Product_Interest, '"', '\"'), '", ',
                                '"Query Type": "', Replace(@query, '"', '\"'), '", ',
                                '"School Name": "', Replace(@_School_Name, '"', '\"'), '", ',
                                '"Title": "', Replace(@_Job_Title, '"', '\"'), '", ',
                                '"User Message": "', Replace(@_User_Message, '"', '\"'), '"}')
                                
            set @jsonPayload = CONCAT('[',@jsonPayload,']')
            var @regionLookup, @SubscriberKey, @EmailAddress

            if not Empty(@_Country_Name) then
              set @regionLookup = Lookup("Country_Region_Mapping", "Region", "Country", @_Country_Name)
            endif

            if @regionLookup == "APAC" then
              set @SubscriberKey = "APAC_CS_EMAIL_ADDR"
              set @TeamName = "APAC Team"
              set @EmailAddress = "customersupport@3plearning.com"
            elseif @regionLookup == "AMER" then
              set @SubscriberKey = "AMER_CS_EMAIL_ADDR"
              set @TeamName = "AMER Team"
              set @EmailAddress = "support.usa@3plearning.com"
            elseif @regionLookup == "EMEA" then
              set @SubscriberKey = "EMEA_CS_EMAIL_ADDR"
              set @TeamName = "EMEA Team"
              set @EmailAddress = "support@3plearning.co.uk"
            elseif @regionLookup == "CANADA" then
              set @SubscriberKey = "CA_CS_EMAIL_ADDR"
              set @TeamName = "Canada Team"
              set @EmailAddress = "customerservice@3plearning.ca"
            else
              set @SubscriberKey = "GLOBAL_CS_EMAIL_ADDR"
              set @TeamName = "Global CS Team"
              set @EmailAddress = "customersupport@3plearning.com"
            endif

            set @Language = "EN"
            set @TriggerSendExternalKey = "229712"

            /* Specify the external key of the TriggerSend */
            SET @TriggerSend = CreateObject("TriggeredSend")
            SET @TriggerSendDefinition = CreateObject("TriggeredSendDefinition")
            SetObjectProperty(@TriggerSendDefinition, "CustomerKey", @TriggerSendExternalKey)
            SetObjectProperty(@TriggerSend, "TriggeredSendDefinition", @TriggerSendDefinition)

            /* Specify the email address and the subscriber key */
            SET @TriggerSendSubscriber = CreateObject("Subscriber")
            SetObjectProperty(@TriggerSendSubscriber, "EmailAddress", @EmailAddress) 
            SetObjectProperty(@TriggerSendSubscriber, "SubscriberKey", @SubscriberKey) 

            /* Fill out the Language field in the TriggerSend data extension */
            SET @TriggerSendLanguage = CreateObject("Attribute")
            SetObjectProperty(@TriggerSendLanguage, "Name", "Language")
            SetObjectProperty(@TriggerSendLanguage,"Value", @Language)
            AddObjectArrayItem(@TriggerSend, "Attributes", @TriggerSendLanguage)

            /* Fill out the FirstName field in the TriggerSend data extension */
            SET @TriggerSendFirstName = CreateObject("Attribute")
            SetObjectProperty(@TriggerSendFirstName, "Name", "TeamName")
            SetObjectProperty(@TriggerSendFirstName,"Value", @TeamName)
            AddObjectArrayItem(@TriggerSend, "Attributes", @TriggerSendFirstName)


            /* Fill out the Payload field in the TriggerSend data extension */
            SET @TriggerSendPayload = CreateObject("Attribute")
            SetObjectProperty(@TriggerSendPayload, "Name", "Payload")
            SetObjectProperty(@TriggerSendPayload,"Value", @jsonPayload)
            AddObjectArrayItem(@TriggerSend, "Attributes", @TriggerSendPayload)


            AddObjectArrayItem(@TriggerSend, "Subscribers", @TriggerSendSubscriber)  

            SET @TriggerSend_statusCode = InvokeCreate(@TriggerSend, @TriggerSend_statusMsg, @errorCode) 

            IF @TriggerSend_statusCode != "OK" THEN
              OUTPUTLINE(CONCAT("Status: ",@TriggerSend_statusMsg," / Code: ",@errorCode))
            ENDIF
          endif

          set @RedirectURL= "https://www.3plearning.com/free-access-events/thank-you/"
          //Redirect(@RedirectURL)
        endif /*debug end*/

]%%

