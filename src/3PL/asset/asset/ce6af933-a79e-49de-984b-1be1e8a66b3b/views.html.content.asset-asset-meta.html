%%[
  SET @DebugEmail = "" /* Leave blank to process all */

  IF NOT EMPTY(@DebugEmail) THEN
    SET @rows = LookupRows("003_Contact_Import_Data_Ready_For_CRM", "Email", @DebugEmail)
  ELSE
    SET @rows = LookupOrderedRows("003_Contact_Import_Data_Ready_For_CRM", 0, "SubmittedDate DESC", "IsProcessed", "false")
  ENDIF

  SET @recordCount = RowCount(@rows)
  SET @debug = false

  IF @recordCount > 0 THEN

    FOR @r = 1 TO @recordCount DO

      SET @mainrow = Row(@rows, @r)
      SET @SubmissionId             = Field(@mainrow, "SubmissionId")
      SET @SubmittedDate            = Field(@mainrow, "SubmittedDate")
      SET @ErrorCode                = Field(@mainrow, "ErrorCode")
      SET @ErrorDetails             = Field(@mainrow, "ErrorDetails")
      SET @IsProcessed              = Field(@mainrow, "IsProcessed")
      SET @contact_FirstName        = Field(@mainrow, "FirstName")
      SET @contact_LastName         = Field(@mainrow, "LastName")
      SET @contact_Email            = Field(@mainrow, "Email")
      SET @contact_Phone            = Field(@mainrow, "Phone")
      SET @contact_MailingCity      = Field(@mainrow, "MailingCity")
      SET @contact_MailingCountry   = Field(@mainrow, "MailingCountry")
      SET @contact_MailingPostalCode= Field(@mainrow, "MailingPostalCode")
      SET @contact_MailingState     = Field(@mainrow, "MailingState")
      SET @contact_MailingStreet    = Field(@mainrow, "MailingStreet")
      SET @contact_Title            = Field(@mainrow, "Title")
      SET @contact_JobFunction      = Field(@mainrow, "Job_Function__c")
      SET @contact_Contact_Source   = Field(@mainrow, "Contact_Source__c")
      SET @contact_CurrencyIsoCode  = Field(@mainrow, "CurrencyIsoCode")
      SET @contact_CampaignName     = Field(@mainrow, "Campaign_Name__c")
      SET @contact_CampaignCode     = Field(@mainrow, "Campaign_Code__c")
      SET @contact_AccountName      = Field(@mainrow, "Company")

      SET @errorMessage = ""

      /* ===================== DEBUG: CONTACT DETAILS ===================== */
      IF @debug == true THEN
        Output(CONCAT(
          "<div style='font-family: Arial, sans-serif; font-size:14px; line-height:1.6; padding:10px; border-bottom:1px solid #ccc;'>",
          "<strong style='color:#444;'>SubmissionId:</strong> <span style='color:#0070c0;'>", @SubmissionId, "</span><br>",
          "<strong style='color:#444;'>SubmittedDate:</strong> <span style='color:#0070c0;'>", @SubmittedDate, "</span><br>",
          "<strong style='color:#444;'>ErrorCode:</strong> <span style='color:#cc0000;'>", @ErrorCode, "</span><br>",
          "<strong style='color:#444;'>ErrorDetails:</strong> <span style='color:#cc0000;'>", @ErrorDetails, "</span><br><br>",

          "<strong style='color:#444;'>Contact_FirstName:</strong> ", @contact_FirstName, "<br>",
          "<strong style='color:#444;'>Contact_LastName:</strong> ", @contact_LastName, "<br>",
          "<strong style='color:#444;'>Contact_Email:</strong> ", @contact_Email, "<br>",
          "<strong style='color:#444;'>Contact_Phone:</strong> ", @contact_Phone, "<br>",
          "<strong style='color:#444;'>Contact_Title:</strong> ", @contact_Title, "<br>",
          "<strong style='color:#444;'>Contact_AccountName:</strong> ", @contact_AccountName, "<br>",
          "<strong style='color:#444;'>Contact_MailingStreet:</strong> ", @contact_MailingStreet, "<br>",
          "<strong style='color:#444;'>Contact_MailingCity:</strong> ", @contact_MailingCity, "<br>",
          "<strong style='color:#444;'>Contact_MailingState:</strong> ", @contact_MailingState, "<br>",
          "<strong style='color:#444;'>Contact_MailingPostalCode:</strong> ", @contact_MailingPostalCode, "<br>",
          "<strong style='color:#444;'>Contact_MailingCountry:</strong> ", @contact_MailingCountry, "<br>",
          "<strong style='color:#444;'>Contact_CampaignName:</strong> ", @contact_CampaignName, "<br>",
          "<strong style='color:#444;'>Contact_CampaignCode:</strong> ", @contact_CampaignCode, "<br>",
          "<hr style='border:none;height:4px;background-color:#FFCC00;margin:20px 0;'>",
          "</div>"
        ))
      ENDIF

      /* ===================== CHECK EXISTING CONTACT ===================== */
      SET @existingContactRows = RetrieveSalesforceObjects("Contact", "Id,Email", "Email", "=", @contact_Email)
      SET @contactFound = RowCount(@existingContactRows)

      IF @contactFound > 0 THEN
        SET @existing_Contact   = Row(@existingContactRows, 1)
        SET @existing_ContactId = Field(@existing_Contact, "Id")
        SET @existing_Email     = Field(@existing_Contact, "Email")

        IF @debug == true THEN
          Output(CONCAT(
            "<div style='font-family: Arial, sans-serif; font-size:14px; line-height:1.6; padding:10px; border-bottom:1px solid #ccc;'>",
            "<strong style='color:#444;'>Existing Contact Found:</strong> âœ…<br>",
            "<strong style='color:#444;'>Existing Contact ID:</strong> ", @existing_ContactId, "<br>",
            "<strong style='color:#444;'>Existing Email:</strong> ", @existing_Email, "<br>",
            "<strong style='color:#444;'>Campaign to Add:</strong> ", @contact_CampaignName, "<br>",
            "<hr style='border:none;height:4px;background-color:#FFCC00;margin:20px 0;'>",
            "</div>"
          ))
        ENDIF

        /* Log in Duplicate Log DE */
        UpsertData("004_Contact_Import_Duplicate_Log", 1,
          "SubmissionId", @SubmissionId,
          "Email", @contact_Email,
          "ExistingContactId", @existing_ContactId,
          "LoggedDate", Now()
        )

        /* ===================== CAMPAIGN MEMBER CREATION ===================== */
      ]%%
        <script runat="server">
          Platform.Load("core", "1");
          try {
        </script>
      %%[
        SET @isCMAlready = RetrieveSalesforceObjects("CampaignMember", "Id", "ContactId", "=", @existing_ContactId)
        IF RowCount(@isCMAlready) > 0 THEN
          IF @debug THEN
            Output(CONCAT("<strong>Already a Campaign Member for this Contact.</strong><br><hr>"))
          ENDIF
        ELSE
          IF NOT EMPTY(@contact_CampaignName) THEN
            SET @_Campaign_Objects = RetrieveSalesforceObjects("Campaign", "Id", "Name", "=", @contact_CampaignName)
            IF RowCount(@_Campaign_Objects) > 0 THEN
              SET @CAMPAIGN_MEMBER__CampaignId = Field(Row(@_Campaign_Objects, 1), "Id")
              SET @CAMPAIGN_MEMBER__New_Id = CreateSalesforceObject("CampaignMember", 2,
                "CampaignId", @CAMPAIGN_MEMBER__CampaignId,
                "ContactId", @existing_ContactId
              )
              IF @debug THEN
                Output(CONCAT("<strong>CampaignMember created successfully.</strong> ID: ", @CAMPAIGN_MEMBER__New_Id, "<br><hr>"))
              ENDIF
            ELSE
              IF @debug THEN
                Output(CONCAT("<strong style='color:red;'>Campaign not found:</strong> ", @contact_CampaignName, "<br><hr>"))
              ENDIF
            ENDIF
          ENDIF
        ENDIF
      ]%%
        <script runat="server">
          } catch (err) {
            var errorMessage = "Error Message: " + Stringify(err.message || err.toString());
            Variable.SetValue("@errorMessage", errorMessage);
          }
        </script>
      %%[
        /* ===================== MARK AS PROCESSED ===================== */
        IF NOT EMPTY(@existing_ContactId) THEN
          UpdateData("003_Contact_Import_Data_Ready_For_CRM", 1, "SubmissionId", @SubmissionId,
            "IsProcessed", "true",
            "ResultStatus", @CAMPAIGN_MEMBER__New_Id,
            "CRMErrorDetails", IIF(Not Empty(@errorMessage), @errorMessage, "")
          )
          UpdateData("002_Contact_Import_Data_InQueue_For_CRM", 1, "SubmissionId", @SubmissionId,
            "IsProcessed", "true",
            "ResultStatus", @CAMPAIGN_MEMBER__New_Id,
            "CRMErrorDetails", IIF(Not Empty(@errorMessage), @errorMessage, "")
          )
        ENDIF

        SET @existing_ContactId = ""
        SET @CAMPAIGN_MEMBER__New_Id = ""

      ELSE
        IF @debug THEN
          Output(CONCAT("<strong style='color:red;'>No existing Contact found for Email:</strong> ", @contact_Email, "<br><hr>"))
        ENDIF
      ENDIF

    NEXT @r

  ELSE
    Output("No unprocessed records found.")
  ENDIF
]%%
