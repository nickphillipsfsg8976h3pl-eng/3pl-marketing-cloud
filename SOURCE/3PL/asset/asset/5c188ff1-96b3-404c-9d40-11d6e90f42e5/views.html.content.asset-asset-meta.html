%%[
    set @debug     = false
    set @submission = false
    set @LogTime       = SystemDateToLocalDate(now())
    IF RequestParameter("submitted") == "true" THEN
        set @submission    = true
        set @customerEmail = RequestParameter("customerEmail")
        set @requestorName = RequestParameter("requestorName")
        set @requestorEmail= RequestParameter("requestorEmail")
        set @LogTime       = SystemDateToLocalDate(now())
         
        IF @debug == true THEN
           OutputLine(Concat('<pre style="background-color:#ccc;"><code>customerEmail: ', @customerEmail, '</code></pre>'))
           OutputLine(Concat('<pre style="background-color:#ccc;"><code>requestorName: ', @requestorName, '</code></pre>'))
           OutputLine(Concat('<pre style="background-color:#ccc;"><code>requestorEmail: ',@requestorEmail, '</code></pre>'))
        ENDIF

        /* Skip processing if email address is empty */
        if not empty(@customerEmail) then
]%%
            <script runat="server">
                Platform.Load("Core","1");
                var emailToLookup = Platform.Variable.GetValue("@customerEmail");
                try {
                    var rr = Platform.Function.LookupRows("Ent._Subscribers", "EmailAddress", emailToLookup);
                    var subscriberKeys = [];
                    if (rr && rr.length > 0) {
                        for (var i = 0; i < rr.length; i++) {
                            subscriberKeys.push(rr[i].SubscriberKey);
                        }
                    }
                    Platform.Variable.SetValue("@SubscriberKeys", subscriberKeys.join(','));
                } catch (e) {
                        Write("An error occurred: " + Stringify(e));
                }
            </script>
%%[
            /* Debugging output */
            if @debug then
                OutputLine(Concat('<pre style="background-color:#ccc;"><code>SUBSCRIBER_KEYS: ', @SubscriberKeys, '</code></pre>'))
            endif

            /* Prepare for unsubscribe actions */
            set @reason = "Requested through Customer Support"
            set @unsuball = 1

            if @unsuball == 1 then
            set @unsub = 1
            set @statusforsubs = "Unsubscribed"
            endif

            set @listid = 187
            set @SUBSCRIBER_KEYS_ROWS = BuildRowSetFromString(@SubscriberKeys, ",")
            set @TOTAL_RECORDS_UPDATED = 0

            /* Process each subscriber key found */
            for @i = 1 to RowCount(@SUBSCRIBER_KEYS_ROWS) do
                    set @EACH_SUBSCRIBER_KEY = Field(Row(@SUBSCRIBER_KEYS_ROWS, @i), 1)
                    /* Log unsubscribe event */
                    set @lue = CreateObject("ExecuteRequest")
                    setObjectProperty(@lue, "Name", "LogUnsubEvent")
                    set @lue_prop = CreateObject("APIProperty")
                    setObjectProperty(@lue_prop, "Name", "SubscriberKey")
                    setObjectProperty(@lue_prop, "Value", @EACH_SUBSCRIBER_KEY)
                    AddObjectArrayItem(@lue, "Parameters", @lue_prop)
                    set @lue_prop = CreateObject("APIProperty")
                    setObjectProperty(@lue_prop, "Name", "ListID")
                    setObjectProperty(@lue_prop, "Value", @listid)
                    AddObjectArrayItem(@lue, "Parameters", @lue_prop)
                    set @lue_prop = CreateObject("APIProperty")
                    setObjectProperty(@lue_prop, "Name", "Reason")
                    setObjectProperty(@lue_prop, "Value", @reason)
                    AddObjectArrayItem(@lue, "Parameters", @lue_prop)
                    set @lue_statusCode = InvokeExecute(@lue, @overallStatus, @requestId)
                    set @Response = Row(@lue_statusCode, 1)
                    set @Status = Field(@Response, "StatusMessage")
                    set @Error = Field(@Response, "ErrorCode")

                    /* Update consent in All Subs */
                    set @actsub = CreateObject("Subscriber")
                    setObjectProperty(@actsub, "SubscriberKey", @EACH_SUBSCRIBER_KEY)
                    setObjectProperty(@actsub, "Status", @statusforsubs)
                    set @options = CreateObject("UpdateOptions")
                    set @save = CreateObject("SaveOption")
                    setObjectProperty(@save, "SaveAction", "UpdateAdd")
                    setObjectProperty(@save, "PropertyName", "*")
                    AddObjectArrayItem(@options, "SaveOptions", @save)
                    set @updateStatusCode = InvokeUpdate(@actsub, @updateStatusMessage, @updateErrorCode, @options)
                    set @status  =""
                    if @updateStatusCode == "OK" then
                            set @status  =Concat(@status,"SUCCESS: SFMC updated successfully;")
                    else
                            set @status  =Concat(@status,"FAILURE: SFMC update failed;")
                    endif
                    /* Check the type of subscriber */
                    set @updaterecord = 0
                    set @updatetype = ""
                    if Substring(@EACH_SUBSCRIBER_KEY, 1, 3) == '003' then
                        set @updatetype = "Contact"
                    elseif Substring(@EACH_SUBSCRIBER_KEY, 1, 3) == '00Q' then
                        set @updatetype = "Lead"
                    endif

                    if not empty(@updatetype) then
                        set @subscriberRows = RetrieveSalesforceObjects(
                                  @updatetype,
                                  "FirstName, LastName, Email",
                                  "Id", "=", @EACH_SUBSCRIBER_KEY )
                        if RowCount(@subscriberRows) > 0 then
                            if @updatetype == "Contact" OR @updatetype == "Lead" then
                                set @updaterecord = UpdateSingleSalesforceObject(@updatetype,
                                         @EACH_SUBSCRIBER_KEY,
                                         'HasOptedOutOfEmail', @unsub)
                                set @TOTAL_RECORDS_UPDATED = @TOTAL_RECORDS_UPDATED + @updaterecord
                            endif
                        else 
                            set @Reason = Concat(@Reason, " - Failed to access record from CRM")
                        endif
                    endif
                    
                    if not empty(@updatetype) and @updaterecord == 1 then
                            set @status  =Concat(@status,"SUCCESS: CRM updated successfully;")
                    elseif not empty(@updatetype) then
                            set @status  =Concat(@status,"FAILURE: CRM update failed;")
                    else 
                            set @status  =Concat(@status,"NA: CRM update not applicable;")
                    endif

                    /* Debugging and logging */
                    if @debug then
                         OutputLine(Concat('<pre style="background-color:#ccc;"><code>EACH_SUBSCRIBER_KEY: ', @EACH_SUBSCRIBER_KEY, '</code></pre>'))
                         OutputLine(Concat('<pre style="background-color:#ccc;"><code>updateStatusCode: ', @updateStatusCode, '</code></pre>'))
                         OutputLine(Concat('<pre style="background-color:#ccc;"><code>updaterecord: ', @updaterecord, '</code></pre>'))
                         OutputLine(Concat('<pre style="background-color:#ccc;"><code>TOTAL_RECORDS_UPDATED: ', @TOTAL_RECORDS_UPDATED, '</code></pre>'))
                    endif

                    set @logtime = SystemDateToLocalDate(now())
                    set @logdate = FormatDate(SystemDateToLocalDate(NOW()), "MM/dd/yyyy")
                    insertData("CSC_UnsubscribeRequests_Log", "SubscriberKey", @EACH_SUBSCRIBER_KEY, "EmailAddress", @customerEmail,"LogDate",@logdate, "LogTime", @logtime, "HasOptedOutOfEmail",@HasOptedOutOfEmail,"Status",@status,"SFMC_UpdateStatusCode",@updateStatusCode , "RecordUpdated", @updaterecord, "Reason", @Reason)
                        
            next @i
            if RowCount(@SUBSCRIBER_KEYS_ROWS) == 0 then
                    set @logtime = SystemDateToLocalDate(now())
                    set @logdate = FormatDate(SystemDateToLocalDate(NOW()), "MM/dd/yyyy")
                    insertData("CSC_UnsubscribeRequests_Log", "SubscriberKey", @emailAddress, "EmailAddress", @customerEmail,"LogDate",@logdate, "LogTime", @logtime, "Status","NO RECORDS FOUND in SFMC","SFMC_UpdateStatusCode","NA" ,"HasOptedOutOfEmail", 0, "RecordUpdated", 0, "Reason", "Cannot find subscriber in SFMC")
            endif

            set @rows = LookupRows("CSC_UnsubscribeRequests_Log","EmailAddress",@customerEmail)
            if RowCount(@rows) >0 and not empty(@requestorEmail) then
                set @SubscriberKey = @requestorEmail
                set @EmailAddress  = @requestorEmail
                Var @jsonPayload


    Set @jsonPayload = Concat('{"Requestor Name": "', Replace(@requestorName, '"', '\"'), '", ',
                              '"Customer Email": "', Replace(@customerEmail, '"', '\"'), '"}')
                              
    SET @jsonPayload = CONCAT('[',@jsonPayload,']')

                SET @Language = "EN"
                SET @TriggerSendExternalKey = "235992"


                /* Specify the external key of the TriggerSend */
                SET @TriggerSend = CreateObject("TriggeredSend")
                SET @TriggerSendDefinition = CreateObject("TriggeredSendDefinition")
                SetObjectProperty(@TriggerSendDefinition, "CustomerKey", @TriggerSendExternalKey)
                SetObjectProperty(@TriggerSend, "TriggeredSendDefinition", @TriggerSendDefinition)

                /* Specify the email address and the subscriber key */
                SET @TriggerSendSubscriber = CreateObject("Subscriber")
                SetObjectProperty(@TriggerSendSubscriber, "EmailAddress", @EmailAddress) 
                SetObjectProperty(@TriggerSendSubscriber, "SubscriberKey", @SubscriberKey) 

                /* Fill out the Language field in the TriggerSend data extension */
                SET @TriggerSendLanguage = CreateObject("Attribute")
                SetObjectProperty(@TriggerSendLanguage, "Name", "Language")
                SetObjectProperty(@TriggerSendLanguage,"Value", @Language)
                AddObjectArrayItem(@TriggerSend, "Attributes", @TriggerSendLanguage)
                
     /* Fill out the Payload field in the TriggerSend data extension */
     SET @TriggerSendPayload = CreateObject("Attribute")
     SetObjectProperty(@TriggerSendPayload, "Name", "Payload")
     SetObjectProperty(@TriggerSendPayload,"Value", @jsonPayload)
     AddObjectArrayItem(@TriggerSend, "Attributes", @TriggerSendPayload)
                AddObjectArrayItem(@TriggerSend, "Subscribers", @TriggerSendSubscriber)  

                SET @TriggerSend_statusCode = InvokeCreate(@TriggerSend, @TriggerSend_statusMsg, @errorCode) 

                IF @TriggerSend_statusCode != "OK" THEN
                OUTPUTLINE(CONCAT("Status: ",@TriggerSend_statusMsg," / Code: ",@errorCode))
                ENDIF
            endif
        endif
    endif


]%%
<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <meta http-equiv="X-UA-Compatible" content="ie=edge">
    <title>Unsubscribe Request Form</title>
    <style>
        body {
            font-family: Arial, sans-serif;
            background: #f4f4f4;
            margin: 0;
            padding: 0;
        }
        .container {
            max-width: 600px;
            margin: 30px auto;
            padding: 20px;
            background-color: #ffffff;
            box-shadow: 0 0 10px rgba(0, 0, 0, 0.1);
        }
        h2 {
            color: #333;
            text-align: center;
        }
        .header-image {
            width: 100%;
            height: auto;
            border-bottom: 1px solid #ddd; /* Optional */
        }
        .form-field {
            margin-bottom: 15px;
        }
        label {
            display: block;
            margin-bottom: 5px;
            color: #666;
        }
        input[type="text"],
        input[type="email"] {
            width: 100%;
            padding: 10px;
            border: 1px solid #ddd;
            border-radius: 4px;
            box-sizing: border-box; /* makes sure padding doesn't affect width */
        }
        button {
            background-color: #5cb85c;
            color: white;
            border: none;
            padding: 10px 20px;
            border-radius: 4px;
            cursor: pointer;
        }
        button:hover {
            background-color: #4cae4c;
        }
        .additional-fields {
            margin-top: 20px;
        }
    </style>
</head>
<body>
    <div class="container">
        <img src="https://image.my.3plearning.com/lib/fe3211717d640479701476/m/1/608a4117-0ee2-407e-963e-d6202d01520d.png" alt="Header Image" class="header-image">
        %%[ IF @submission == true THEN]%%
        <p>Thank you for submitting the unsubscribe details! We've successfully received the information. 
        </p>
        %%[ELSE]%%
        <h2>Customer Service - Unsubscribe Request Form </h2>
        <form id="unsubscribeForm" action="%%= RequestParameter('PAGEURL') =%%" method="post">
            <div class="form-field">
                <label for="customerEmail">Customer's Email Address:</label>
                <input type="email" id="customerEmail" name="customerEmail" required>
            </div>
            <div class="form-field">
                <label for="requestorName">Requestor's Name:</label>
                <input type="text" id="requestorName" name="requestorName" required>
            </div>
            <div class="form-field">
                <label for="requestorEmail">Requestor's Email Address:</label>
                <input type="email" id="requestorEmail" name="requestorEmail" required>
            </div>
            
            <div class="form-field">
                <input type="hidden" name="submitted" value="true">
                <button type="submit">Submit Unsubscribe Request</button>
            </div>
        </form>
         <br>
        %%[ENDIF]%%
       
              <p>All the identities with the Customer's email will be unsubscribed from SFMC and CRM platforms. Once unsubscribe automation finishes, you'll receive an automated email with the request's status.</p><p>Please use the same form for multiple unsubscribe requests.</p><br>
      <strong><p>Thank you,</p></strong>
      <strong><p>3P Team</p></strong>
    </div>
    <script>
        function addNewField() {
            var container = document.getElementById('additionalFields');
            var input = document.createElement('input');
            input.type = 'text';
            input.name = 'additionalInfo[]';
            input.placeholder = 'Enter additional information';
            input.style.marginTop = '10px';
            input.required = true; // Remove if not required
            container.appendChild(input);
        }
    </script>
</body>
</html>
