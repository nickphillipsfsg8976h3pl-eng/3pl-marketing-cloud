%%[

  set @DebugEmail = "" /* Leave blank to process all */

  if not empty(@DebugEmail) then
    set @rows = LookupRows("001_Lead_Procesing_Queue_CRM_LAST_3DAYS", "Email", @DebugEmail)
  else
    set @rows = LookupOrderedRows("001_Lead_Procesing_Queue_CRM_LAST_3DAYS", 0, "SubmittedDate DESC", "IsProcessed", "false")
  endif
  
  set @recordCount = RowCount(@rows)
  set @debug = false

  if @recordCount > 0 then

    for @r = 1 to @recordCount do


      set @mainrow = Row(@rows, @r)
      set @SubmissionId  = Field(@mainrow, "SubmissionId")
      set @SubmittedDate = Field(@mainrow, "SubmittedDate")
      set @jsonStr       = Field(@mainrow, "SubmittedData")
      set @ErrorCode     = Field(@mainrow, "ErrorCode")
      set @ErrorDetails  = Field(@mainrow, "ErrorDetails")
      set @IsProcessed   = Field(@mainrow, "IsProcessed")

      /* Parse flat JSON object */

      set @cleanJson = Replace(@jsonStr, ':null', ':""')
      set @cleanJson = Replace(@cleanJson, ': null', ':""')

      /* Build Rowset from cleaned JSON */
      set @jsonParsed = BuildRowSetFromJSON(@cleanJson, "$.rows.[*]", 1)

      

      if RowCount(@jsonParsed) > 0 then
          /* Reset variables before JSON parse */
          var @FormName, @Form, @SubmitterName, @SubmitterNotes, @FirstName, @LastName,
              @Email, @Referrer, @Phone, @Country, @School, @State, @JobTitle, @GradeLevel,
              @CampaignId, @PostCode, @Licenses, @ProductInterest, @EInterest, @MathleticsSubscriber,
              @Subject, @PID, @CID, @GCLID, @FBCLID, @MSCLKID,
              @UTM_Source, @UTM_Medium, @UTM_Campaign, @UTM_Content, @UTM_Term,
              @TermsAccepted, @SubscriberOptIn

          /* Initialize all to empty string to prevent persistence */
          set @FormName = ""
          set @Form = ""
          set @SubmitterName = ""
          set @SubmitterNotes = ""
          set @FirstName = ""
          set @LastName = ""
          set @Email = ""
          set @Referrer = ""
          set @Phone = ""
          set @Country = ""
          set @School = ""
          set @State = ""
          set @JobTitle = ""
          set @GradeLevel = ""
          set @CampaignId = ""
          set @PostCode = ""
          set @Licenses = ""
          set @ProductInterest = ""
          set @EInterest = ""
          set @MathleticsSubscriber = ""
          set @Subject = ""
          set @PID = ""
          set @CID = ""
          set @GCLID = ""
          set @FBCLID = ""
          set @MSCLKID = ""
          set @UTM_Source = ""
          set @UTM_Medium = ""
          set @UTM_Campaign = ""
          set @UTM_Content = ""
          set @UTM_Term = ""
          set @TermsAccepted = ""
          set @SubscriberOptIn = ""


          set @row = Row(@jsonParsed, 1)

          set @FormName             = IIF(IndexOf(@jsonStr, "FormName") > 0, Field(@row, "FormName"), "")
          set @Form                 = IIF(IndexOf(@jsonStr, "Form") > 0, Field(@row, "Form"), "")
          set @SubmitterName        = IIF(IndexOf(@jsonStr, "SubmitterName") > 0, Field(@row, "SubmitterName"), "")
          set @SubmitterNotes        = IIF(IndexOf(@jsonStr, "SubmitterNotes") > 0, Field(@row, "SubmitterNotes"), "")
          set @FirstName            = IIF(IndexOf(@jsonStr, "FirstName") > 0, Field(@row, "FirstName"), "")
          set @LastName             = IIF(IndexOf(@jsonStr, "LastName") > 0, Field(@row, "LastName"), "")
          set @Email                = IIF(IndexOf(@jsonStr, "Email") > 0, Field(@row, "Email"), "")
          set @Referrer             = IIF(IndexOf(@jsonStr, "Referrer") > 0, Field(@row, "Referrer"), "")
          set @Phone                = IIF(IndexOf(@jsonStr, "Phone") > 0, Field(@row, "Phone"), "")
          set @Country              = IIF(IndexOf(@jsonStr, "Country") > 0, Field(@row, "Country"), "")
          set @School               = IIF(IndexOf(@jsonStr, "School") > 0, Field(@row, "School"), "")
          set @State                = IIF(IndexOf(@jsonStr, "State") > 0, Field(@row, "State"), "")
          set @JobTitle             = IIF(IndexOf(@jsonStr, "JobTitle") > 0, Field(@row, "JobTitle"), "")
          set @GradeLevel           = IIF(IndexOf(@jsonStr, "GradeLevel") > 0, Field(@row, "GradeLevel"), "")
          set @CampaignId           = IIF(IndexOf(@jsonStr, "CampaignId") > 0, Field(@row, "CampaignId"), "")
          set @PostCode             = IIF(IndexOf(@jsonStr, "PostCode") > 0, Field(@row, "PostCode"), "")
          set @Licenses             = IIF(IndexOf(@jsonStr, "Licenses") > 0, Field(@row, "Licenses"), "")
          set @ProductInterest      = IIF(IndexOf(@jsonStr, "ProductInterest") > 0, Field(@row, "ProductInterest"), "")
          if @CampaignId == '701Mp00000ITj95IAD' or @CampaignId == '701Mp00000fkC0sIAE' or @CampaignId == '701Mp00000fkQmHIAU' then
          set @EInterest           = IIF(IndexOf(@jsonStr, "Interest") > 0, Field(@row, "Interest"), "")
          endif
          set @MathleticsSubscriber = IIF(IndexOf(@jsonStr, "MathleticsSubscriber") > 0, Field(@row, "MathleticsSubscriber"), "")
          set @Subject              = IIF(IndexOf(@jsonStr, "Subject") > 0, Field(@row, "Subject"), "")
          set @PID                  = IIF(IndexOf(@jsonStr, "PID") > 0, Field(@row, "PID"), "")
          set @CID                  = IIF(IndexOf(@jsonStr, "CID") > 0, Field(@row, "CID"), "")
          set @GCLID                = IIF(IndexOf(@jsonStr, "GCLID") > 0, Field(@row, "GCLID"), "")
          set @FBCLID               = IIF(IndexOf(@jsonStr, "FBCLID") > 0, Field(@row, "FBCLID"), "")
          set @MSCLKID              = IIF(IndexOf(@jsonStr, "MSCLKID") > 0, Field(@row, "MSCLKID"), "")
          set @UTM_Source           = IIF(IndexOf(@jsonStr, "UTM_Source") > 0, Field(@row, "UTM_Source"), "")
          set @UTM_Medium           = IIF(IndexOf(@jsonStr, "UTM_Medium") > 0, Field(@row, "UTM_Medium"), "")
          set @UTM_Campaign         = IIF(IndexOf(@jsonStr, "UTM_Campaign") > 0, Field(@row, "UTM_Campaign"), "")
          set @UTM_Content          = IIF(IndexOf(@jsonStr, "UTM_Content") > 0, Field(@row, "UTM_Content"), "")
          set @UTM_Term             = IIF(IndexOf(@jsonStr, "UTM_Term") > 0, Field(@row, "UTM_Term"), "")
          set @TermsAccepted        = IIF(IndexOf(@jsonStr, "TermsAccepted") > 0, Field(@row, "TermsAccepted"), "")
          set @SubscriberOptIn      = IIF(IndexOf(@jsonStr, "SubscriberOptIn") > 0, Field(@row, "SubscriberOptIn"), "")


          /* UTM Source */
          set @utm_src_raw = @UTM_Source
          set @UTM_Source  = ""            
          set @src_rs = BuildRowsetFromString(Replace(@utm_src_raw,";",","), ",")
          for @i = 1 TO RowCount(@src_rs) do
              set @v = Trim(Field(Row(@src_rs,@i),1))
              if NOT EMPTY(@v) AND IndexOf(Concat(",",@UTM_Source,","), Concat(",",@v,",")) == 0 then
                set @UTM_Source = IIF(EMPTY(@UTM_Source), @v, Concat(@UTM_Source, ",", @v))
              endif
          next @i

          /* UTM Campaign */
          set @utm_cmp_raw = @UTM_Campaign
          set @UTM_Campaign = ""
          set @cmp_rs = BuildRowsetFromString(Replace(@utm_cmp_raw,";",","), ",")
          FOR @i = 1 TO RowCount(@cmp_rs) DO
            set @v = Trim(Field(Row(@cmp_rs,@i),1))
            IF NOT EMPTY(@v) AND IndexOf(Concat(",",@UTM_Campaign,","), Concat(",",@v,",")) == 0 THEN
              set @UTM_Campaign = IIF(EMPTY(@UTM_Campaign), @v, Concat(@UTM_Campaign, ",", @v))
            ENDIF
          NEXT @i
          
          /* UTM Term */
          set @utm_term_raw = @UTM_Term
          set @UTM_Term = ""
          set @term_rs = BuildRowsetFromString(Replace(@utm_term_raw,";",","), ",")
          FOR @i = 1 TO RowCount(@term_rs) DO
            set @v = Trim(Field(Row(@term_rs,@i),1))
            IF NOT EMPTY(@v) AND IndexOf(Concat(",",@UTM_Term,","), Concat(",",@v,",")) == 0 THEN
              set @UTM_Term = IIF(EMPTY(@UTM_Term), @v, Concat(@UTM_Term, ",", @v))
            ENDIF
          NEXT @i

          /* UTM Medium */
          set @utm_med_raw = @UTM_Medium
          set @utm_med_raw = Replace(Replace(@utm_med_raw,";",","), ",,", ",")
          set @UTM_Medium = ""
          set @med_rs = BuildRowsetFromString(@utm_med_raw, ",")

          FOR @i = 1 TO RowCount(@med_rs) DO
            set @v = Trim(Field(Row(@med_rs,@i),1))
            IF NOT EMPTY(@v) AND IndexOf(Concat(",", @UTM_Medium, ","), Concat(",", @v, ",")) == 0 THEN
               set @UTM_Medium = IIF(EMPTY(@UTM_Medium), @v, Concat(@UTM_Medium, ",", @v))
            ENDIF
          NEXT @i

          /* --- Check if 'test' is in name or email --- */
          set @is_invalid_lead = "false"
          if indexof(@ErrorCode, "TEST_KEYWORDS") > 0 then
            set @is_invalid_lead = "true"
          endif

          /* Additional processing required for lead creation */

          /*Job Function*/
          set @_JobFunction   = Lookup("JobTitle_Mapping_JobFunction_In_SF", "SF Job Function", "Job Title", @JobTitle)
          if empty(@_JobFunction) then
            set @_JobFunction = Lookup("jobTitle_USA_District_Forms", "SF Job Function", "Job Title", @JobTitle)
          endif

          /*State and Country*/
          var @_StateName
          set @countrycode = Lookup("Country_DE","CountryCode","CountryName", @Country)
          if not empty(@countrycode) then
            set @rows1        = LookupRows("state","State Code", @State,"Country Code", @countrycode)
            set @rowCount1    = rowcount(@rows1)

            if @rowCount1 > 0 then
              for @i = 1 to @rowCount1 do

                set @row1 = row(@rows1, @i) /* get row based on counter */
                set @_StateName = field(@row1,"State Name")
              next @i 
            endif
          endif

          /*Region and Territory*/
          set @Region      = Lookup("Country_Region_Mapping", "Region", "Country", @Country)
          set @_Territory  =""
          if not empty(@region) then
            set @_Territory  = IIF(@Region == 'EMEA', 'EME', IIF(@Region == 'APAC', 'APAC', IIF(@Region == 'AMER' or @Region == 'CANADA', 'AMER', '')))
          endif

          /*Subject*/
          set @_FormattedSubjects = IIF(Not Empty(@Subject), Concat(Replace(@Subject, ",", ";"), ";"), "")

          /*Campaign Details*/

          var @campaignNameLookup, @campaignCodeLookup, @UTMcampaignIdLookup, @UTMcampaignCodeLookup
          var @Campaign_Name_Lead, @Campaign_Code, @UTM_Campaign_entry

          /* Primary campaign lookup */
          if not empty(@CampaignId) then
            set @campaignNameLookup = Lookup("ENT.Campaign_Salesforce", "Name", "Id", @CampaignId)
            set @campaignCodeLookup = Lookup("ENT.Campaign_Salesforce", "Campaign_Code__c", "Id", @CampaignId)
            set @Campaign_Name_Lead = IIF(not empty(@campaignNameLookup), @campaignNameLookup, "NOT-DEFINED")
            set @Campaign_Code = IIF(not empty(@campaignCodeLookup), @campaignCodeLookup, "NOT-DEFINED")
          endif

          /* UTM campaign overrides */
          if @Form != "tof" and @UTM_Campaign!='Organic' then
            set @UTMcampaignIdLookup = Lookup("ENT.Campaign_Salesforce", "Id", "Name", @UTM_Campaign)
            set @UTMcampaignCodeLookup = Lookup("ENT.Campaign_Salesforce", "Campaign_Code__c", "Name", @UTM_Campaign)
            set @Campaign_Name_Lead = @UTM_Campaign           
            set @Campaign_Code = IIF(not empty(@UTMcampaignCodeLookup), @UTMcampaignCodeLookup, @Campaign_Code)
          endif
          
          if @Form != "tof" and @UTM_Campaign !='Organic' then
             if not empty(@UTMcampaignCodeLookup) then
               set @UTM_Campaign_entry= @UTM_Campaign
             else
               set @UTM_Campaign_entry= @campaignNameLookup
             endif
          endif

          if @UTM_Campaign == 'Organic' then
            set @UTM_Campaign_entry= @campaignNameLookup
          endif
         
          /* TOF-specific resource campaign mapping */
          set @region = Lookup("Country_Region_Mapping", "Region", "Country", @Country)

          var @EMEA_CampaignID, @EMEA_CampaignName, @EMEA_CampaignCode
          var @AMER_CampaignID, @AMER_CampaignName, @AMER_CampaignCode
          var @APAC_CampaignID, @APAC_CampaignName, @APAC_CampaignCode

          if @form == "tof" then

            if @region == "EMEA" then
              set @campaign_rows = LookupRows("Resources_Campaign_Mapping", "Global_CampaignID", @CampaignId)
              set @rowCount = rowcount(@campaign_rows)

              if @rowCount == 1 then
                var @emailAddress, @firstName, @rank
                set @row = row(@campaign_rows, 1)
                set @EMEA_CampaignID   = field(@row, "EMEA_CampaignID")
                set @EMEA_CampaignName = field(@row, "EMEA_CampaignName")
                set @EMEA_CampaignCode = field(@row, "EMEA_CampaignCode")
              endif

              set @Campaign_Name_Lead = IIF(empty(@EMEA_CampaignID), @CampaignId, @EMEA_CampaignID)
              set @Campaign_Code      = IIF(empty(@EMEA_CampaignCode), @Campaign_Code, @EMEA_CampaignCode)
              set @pid                = IIF(empty(@EMEA_CampaignID), @pid, "ReadingEggs")

            elseif @region == "AMER" or @region == "CANADA" then
              set @campaign_rows = LookupRows("Resources_Campaign_Mapping_AMER", "Global_CampaignID", @CampaignId)
              set @rowCount = rowcount(@campaign_rows)

              if @rowCount == 1 then
                var @emailAddress, @firstName, @rank
                set @row = row(@campaign_rows, 1)
                set @AMER_CampaignID   = field(@row, "AMER_CampaignID")
                set @AMER_CampaignName = field(@row, "AMER_CampaignName")
                set @AMER_CampaignCode = field(@row, "AMER_CampaignCode")
              endif

              set @Campaign_Name_Lead = IIF(empty(@AMER_CampaignID), @CampaignId, @AMER_CampaignID)
              set @Campaign_Code      = IIF(empty(@AMER_CampaignCode), @Campaign_Code, @AMER_CampaignCode)

            elseif @region == "APAC" then
              set @campaign_rows = LookupRows("Resources_Campaign_Mapping_APAC", "Global_CampaignID", @CampaignId)
              set @rowCount = rowcount(@campaign_rows)

              if @rowCount == 1 then
                var @emailAddress, @firstName, @rank
                set @row = row(@campaign_rows, 1)
                set @APAC_CampaignID   = field(@row, "APAC_CampaignID")
                set @APAC_CampaignName = field(@row, "APAC_CampaignName")
                set @APAC_CampaignCode = field(@row, "APAC_CampaignCode")
              endif

              set @Campaign_Name_Lead = IIF(empty(@APAC_CampaignID), @CampaignId, @APAC_CampaignID)
              set @Campaign_Code      = IIF(empty(@APAC_CampaignCode), @Campaign_Code, @APAC_CampaignCode)
            endif

            set @UTM_Campaign_entry = IIF(not empty(@Campaign_Name_Lead), @Campaign_Name_Lead, @campaignNameLookup)
          endif
          
          /* Check if campaign name needs lookup */
          if not empty(@UTM_Campaign_entry) then
            if Substring(@UTM_Campaign_entry,1,3) == '701' then
              set @UTM_Campaign_entry = Lookup("ENT.Campaign_Salesforce", "Name", "Id", @UTM_Campaign_entry)
            endif
          endif



          /*Description*/
          set @LicenseLabel =""
          if not empty(@Licenses) then
            set @LicenseLabel      = IIF(@Licenses > 1, 'licenses', 'license')
          endif
          if @Form == "quote" and Not Empty(@Licenses) then
            set @_Description     = Concat("Customer has requested a quote for ", @Licenses, " ", @LicenseLabel, ".")
          elseif @Form == "trial" then
            set @_Description     = "Customer has requested a Trial."
          elseif @Form == "demo" then
            set @_Description     = "Customer has requested a Demo."
          elseif @Form == "info" then
            set @_Description     = "Customer has requested Information."
          else
            set @_Description     = "Customer has requested a resource."
          endif
          
          
          if @FormName == "csc_crm_lead_processing_form" and Not Empty(@Licenses) then
            set @_Description     = Concat("Lead has been submitted by customer service requesting a quote for ", @Licenses, " ", @LicenseLabel, ".")
          elseif @FormName == "csc_crm_lead_processing_form" and @Form == "trial" then
            set @_Description     = "Lead has been submitted by customer service requesting a Trial."
          elseif @FormName == "csc_crm_lead_processing_form" and @Form == "demo" then
            set @_Description     = "Lead has been submitted by customer service requesteding a Demo."
          elseif @FormName == "csc_crm_lead_processing_form" and @Form == "info" then
            set @_Description     = "Lead has been submitted by customer service requesteding Information."
          endif
          

          if Not Empty(@SubmitterName) then
            set @_Description = Concat(@_Description,CHAR(10), CHAR(13),CHAR(10), CHAR(13), "Submitted by: ", @SubmitterName)
          endif

          if Not Empty(@SubmitterNotes) then
            set @_Description = Concat(@_Description,CHAR(10), CHAR(13),  "Additional Context:",CHAR(10), CHAR(13),  @SubmitterNotes)
          endif
          
          /*Enquiry_Type*/
          if @Form == 'quote' then 
            set @_Enquiry_Type = 'Quote' 
          elseif @Form == 'trial' then
            set @_Enquiry_Type = 'Trial' 
          elseif @Form == 'demo' then
            set @_Enquiry_Type = 'Demo'
          elseif @Form == 'info' then
            set @_Enquiry_Type = 'Information'  
          else
            set @_Enquiry_Type = ''
          endif

          set @ExistingCustomer =""
          if not empty(@MathleticsSubscriber) then
            set @ExistingCustomer = IIF(@MathleticsSubscriber == "Yes", "Yes", "No")
          endif

          set @errorMessage = ""

          set @PID                     = IIF(not empty(@PID), @PID, @ProductInterest)
          if not empty(@PID) and @FormName!='csc_crm_lead_processing_form' then
            set @_Mkt_Product_Interest   = Replace(@PID,"brightpath","Brightpath Writing")
            set @_Mkt_Product_Interest   = Replace(@_Mkt_Product_Interest,",",";")
            set @_Product_Interest_c     = Replace(@PID,"brightpath","Brightpath Progress Writing")
            set @_Product_Interest_c     = Replace(@_Product_Interest_c,",",";")
            set @PID                     = Replace(@PID,"brightpath","Brightpath Writing")
            set @PID1                    = Replace(@PID,"Brightpath Writing","Brightpath Progress Writing")
          endif

          if not empty(@PID) and @FormName=='csc_crm_lead_processing_form' then
            set @_Mkt_Product_Interest   = Replace(@PID,"Brightpath Progress Writing","Brightpath Writing")
            set @_Mkt_Product_Interest   = Replace(@_Mkt_Product_Interest,"Brightpath Progress Maths","Brightpath Maths")
            set @_Mkt_Product_Interest   = Replace(@_Mkt_Product_Interest,"LiteracyPackage","")
            set @_Mkt_Product_Interest   = Replace(@_Mkt_Product_Interest,"MathematicsPackage","")
            set @_Mkt_Product_Interest   = Replace(@_Mkt_Product_Interest,",",";")
            set @_Product_Interest_c     = @PID
            set @_Product_Interest_c     = Replace(@_Product_Interest_c,",",";")
            set @PID                     = Replace(@PID,"brightpath","Brightpath Writing")
            set @PID1                    = Replace(@PID,"Brightpath Writing","Brightpath Progress Writing")
          endif

          if @Form == 'tof' then
            set @LeadStatus = "Marketing Prospect"
          else
            set @LeadStatus = "MQL"
          endif


          /*Adhoc Processing*/
          if not empty(@CampaignId) then
            if @CampaignId == '701Mp00000ITj95IAD' and not empty(@EInterest) then
              set @interest = Lowercase(Trim(Replace(Replace(Replace(@EInterest, ";", ","), " ", ""), ",,", ",")))

              /* Flags */
              set @hasQuote = IIF(IndexOf(@interest, "quote") > 0, 1, 0)
              set @hasDemo  = IIF(IndexOf(@interest, "demo")  > 0, 1, 0)

              set @threeEUrl = "https://www.3plearning.com/3essentials/"

              /* Build the base description from interest */
              IF @hasQuote == 1 AND @hasDemo == 1 THEN
                set @base = Concat("Lead has requested a QUOTE & DEMO via ", @threeEUrl)
              ELSEIF @hasQuote == 1 THEN
                set @base = Concat("Lead has requested a QUOTE via ", @threeEUrl)
              ELSEIF @hasDemo == 1 THEN
                set @base = Concat("Lead has requested a DEMO via ", @threeEUrl)
              ELSE
                /* Fallback if nothing selected */
                set @base = Concat("Lead submitted interest via ", @threeEUrl)
              ENDIF

              /* ----- Optional: append your licence/student details ----- */
              VAR @license_pluralisation
              IF @Licenses > 0 THEN
                set @license_pluralisation = IIF(@Licenses > 1, "licenses", "license")
                set @base = Concat(@base, ". Customer has requested a quote for ", @Licenses, " ", @license_pluralisation)
              ENDIF

              /* Final description */
              set @_Description = @base

              if @hasQuote == 1 then 
                 set @_Enquiry_Type = 'Quote' 
              elseif @hasDemo == 1 then
                 set @_Enquiry_Type = 'Demo'
              else
                set @_Enquiry_Type = ''
              endif
            endif
          endif
          
          /*Adhoc Processing 3E_Customer_Dashboard and 3P_Customer_Dashboard*/
          if not empty(@CampaignId) then
            if (@CampaignId == '701Mp00000fkC0sIAE' or @CampaignId == '701Mp00000fkQmHIAU') and not empty(@EInterest) then
              set @interest = Lowercase(Trim(Replace(Replace(Replace(@EInterest, ";", ","), " ", ""), ",,", ",")))

              set @hasQuote = IIF(IndexOf(@interest, "quote") > 0, 1, 0)
              set @hasDemo  = IIF(IndexOf(@interest, "demo")  > 0, 1, 0)
              set @hasTrial = IIF(IndexOf(@interest, "trial") > 0, 1, 0)

              if @CampaignId == '701Mp00000fkC0sIAE' then
                set @threeEUrl = "3E_Customer_Dashboard"
              else
                set @threeEUrl = "3P_Customer_Dashboard"
              endif

              /* Build the base description from interest */
              IF @hasQuote == 1 AND @hasDemo == 1 AND @hasTrial == 1 THEN
                set @base = Concat("Lead has requested a QUOTE, DEMO & TRIAL via ", @threeEUrl)
              ELSEIF @hasQuote == 1 AND @hasDemo == 1 THEN
                set @base = Concat("Lead has requested a QUOTE & DEMO via ", @threeEUrl)
              ELSEIF @hasQuote == 1 AND @hasTrial == 1 THEN
                set @base = Concat("Lead has requested a QUOTE & TRIAL via ", @threeEUrl)
              ELSEIF @hasDemo == 1 AND @hasTrial == 1 THEN
                set @base = Concat("Lead has requested a DEMO & TRIAL via ", @threeEUrl)
              ELSEIF @hasQuote == 1 THEN
                set @base = Concat("Lead has requested a QUOTE via ", @threeEUrl)
              ELSEIF @hasDemo == 1 THEN
                set @base = Concat("Lead has requested a DEMO via ", @threeEUrl)
              ELSEIF @hasTrial == 1 THEN
                set @base = Concat("Lead has requested a TRIAL via ", @threeEUrl)
              ELSE
                /* Fallback if nothing selected */
                set @base = Concat("Lead submitted interest via ", @threeEUrl)
              ENDIF

              /* Enquiry_Type */
              IF @hasQuote == 1 THEN 
                set @_Enquiry_Type = 'Quote' 
              ELSEIF @hasDemo == 1 THEN
                set @_Enquiry_Type = 'Demo'
              ELSEIF @hasTrial == 1 THEN
                set @_Enquiry_Type = 'Trial'
              ELSE
                set @_Enquiry_Type = ''
              ENDIF

              /* ----- Optional: append your licence/student details ----- */
              VAR @license_pluralisation
              IF @Licenses > 0 THEN
                set @license_pluralisation = IIF(@Licenses > 1, "licenses", "license")
                set @base = Concat(@base, ". Customer has requested a quote for ", @Licenses, " ", @license_pluralisation)
              ENDIF

              /* Final description */
              set @_Description = @base

            endif
          endif
          
          /*Adhoc Processing 3E_EMEA_FY26_MAT_pilot_LP*/
          if not empty(@CampaignId) then
            if @CampaignId == '701Mp00000ePwWQIA0' then

              set @threeEUrl = "https://www.3plearning.com/3essentials/uk/pilot/"

              /* Build the base description from interest */
              set @base = Concat("Lead has been submitted on ", @threeEUrl,", requesting a callback to discuss piloting across their MAT.")
              
              VAR @license_pluralisation
              IF @Licenses > 0 THEN
                set @license_pluralisation = IIF(@Licenses > 1, "schools", "school")
                set @base = Concat(@base, " Schools in MAT: ", @Licenses, " ", @license_pluralisation)
              ENDIF

              /* Final description */
              set @_Description         = @base
              set @Licenses             = ""
              set @_Enquiry_Type        = "Information"
              set @_Product_Interest_c  = "3 Essentials Package"
              set @_JobFunction         = "District/Mat Leader"
              set @Country              = "United Kingdom"

            endif
          endif

          /* 1. Preprocess Variables - Underscored variable names are further transformed*/
                
          set @lead_FirstName         = IIF(Not Empty(@FirstName), @FirstName, "")
          set @lead_LastName          = IIF(Not Empty(@LastName), @LastName, "NA")
          set @lead_Email             = IIF(Not Empty(@Email), @Email, "xxxx@xxxx.com")
          set @lead_Phone             = IIF(Not Empty(@Phone), @Phone, "0000000000")
          set @lead_GradeLevel        = IIF(Not Empty(@GradeLevel), @GradeLevel, "Not Mentioned")
          set @lead_Title             = IIF(Not Empty(@JobTitle), @JobTitle, "Unknown")          
          set @lead_Country           = IIF(Not Empty(@Country), @Country, "Australia")          
          set @lead_PostalCode        = IIF(Not Empty(@PostCode), @PostCode, "0000")
          set @lead_Company           = IIF(Not Empty(@School), @School, "NA Currently")
          set @lead_Licenses          = IIF(Not Empty(@Licenses), @Licenses, 0)          
          set @lead_UTMSource         = IIF(Not Empty(@UTM_Source), @UTM_Source, 'Unknown')
          set @lead_UTMMedium         = IIF(Not Empty(@UTM_Medium), @UTM_Medium, "Unknown")
          set @lead_UTMCampaign       = IIF(Not Empty(@UTM_Campaign), @UTM_Campaign, "Organic")
          set @lead_UTMContent        = IIF(Not Empty(@UTM_Content), @UTM_Content, "Unknown")
          set @lead_UTMTerm           = IIF(Not Empty(@UTM_Term), @UTM_Term, "Unknown")          
          set @lead_GCLID             = IIF(Not Empty(@GCLID), @GCLID, "Unknown")
          set @lead_FBCLID            = IIF(Not Empty(@FBCLID), @FBCLID, "Unknown")
          set @lead_MSCLKID           = IIF(Not Empty(@MSCLKID), @MSCLKID, "Unknown")
          set @lead_MarketingInterest = ""
          if not empty(@SubscriberOptIn) then
            set @lead_MarketingInterest = IIF(@SubscriberOptIn == "true", "Promotions;Product Information;Newsletter", "Promotions;Product Information;")
          endif
          set @lead_Source            = "Marketing Cloud"
          set @lead_Status            = @LeadStatus
          set @lead_ExistingCustomer  = ""
          if not empty(@MathleticsSubscriber) then
            set @lead_ExistingCustomer  = IIF(@MathleticsSubscriber == "Yes", "Yes", "No")
          endif
          set @lead_JobFunction       = IIF(Not Empty(@_JobFunction), @_JobFunction, "")
          set @lead_Description       = IIF(Not Empty(@_Description), @_Description, "Unknown")
          set @lead_EnquiryType       = @_Enquiry_Type
          set @lead_State             = IIF(Not Empty(@_StateName), @_StateName, "")
          set @lead_State_c           = IIF(Not Empty(@_StateName), @_StateName, "")
          set @lead_Subject           = IIF(Not Empty(@_FormattedSubjects), @_FormattedSubjects, "")
          set @lead_CampaignName      = IIF(Not Empty(@UTM_Campaign_entry), @UTM_Campaign_entry, "")
          set @lead_CampaignCode      = IIF(Not Empty(@Campaign_Code), @Campaign_Code, "")          
          set @lead_ProductInterest   = @_Product_Interest_c
          set @lead_MktProductInterest= @_Mkt_Product_Interest                    
          set @lead_Territory         = @_Territory
          set @lead_isInvalid         = @is_invalid_lead

          UpsertData("002_Lead_Ready_For_CRM_Last_3Days", 1,
            "SubmissionId", @SubmissionId,
            "SubmittedDate", @SubmittedDate,
            "ErrorDetails", @ErrorDetails,
            "ErrorCode", @ErrorCode,
            "IsProcessed", @IsProcessed,
            "FirstName", @lead_FirstName,
            "LastName", @lead_LastName,
            "Email", @lead_Email,
            "Phone", @lead_Phone,
            "Grade_Level__c", @lead_GradeLevel,
            "Title", @lead_Title,
            "Country", @lead_Country,
            "PostalCode", @lead_PostalCode,
            "Company", @lead_Company,
            "No_of_licences__c", @lead_Licenses,
            "UTM_Source__c", @lead_UTMSource,
            "UTM_Medium__c", @lead_UTMMedium,
            "UTM_Campaign__c", @lead_UTMCampaign,
            "UTM_Content__c", @lead_UTMContent,
            "UTM_Term__c", @lead_UTMTerm,
            "GCLID__c", @lead_GCLID,
            "FBCLID__c", @lead_FBCLID,
            "MSCLKID__c", @lead_MSCLKID,
            "Marketing_Interest__c", @lead_MarketingInterest,
            "LeadSource", @lead_Source,
            "Status", @lead_Status,
            "Existing_Customer__c", @lead_ExistingCustomer,
            "Job_Function__c", @lead_JobFunction,
            "Description", @lead_Description,
            "Enquiry_Type__c", @lead_EnquiryType,
            "State", @lead_State,
            "State__c", @lead_State_c,
            "Subject__c", @lead_Subject,
            "Campaign_Name__c", @lead_CampaignName,
            "Campaign_Code__c", @lead_CampaignCode,
            "Product_Interest__c", @lead_ProductInterest,
            "Marketing_Product_Interest__c", @lead_MktProductInterest,
            "Territory__c", @lead_Territory,
            "Invalid_Lead__c", @lead_isInvalid,
            "FormCampaignId",@CampaignId
          )




      else
        output(concat("<br>Could not parse JSON for SubmissionId: ", @SubmissionId))
      endif


    next @r

  else
    output("No unprocessed records found.")
  endif
]%%
