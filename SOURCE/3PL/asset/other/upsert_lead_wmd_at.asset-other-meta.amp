<script runat="server">
Platform.Load("core","1");
try {
</script>
%%[
    /* Create Lead */
    SET @LEAD__New_Id = CreateSalesforceObject("Lead", 31,
        "Marketing_Product_Interest__c", @_Product_Interest, 
        "FirstName", IIF(Not Empty(@_First_Name), @_First_Name, ""),  
        "LastName", IIF(Not Empty(@_Last_Name), @_Last_Name, "NA"),  
        "Email", IIF(Not Empty(@_Email_Address), @_Email_Address, "xxxx@xxxx.com"), 
        "Phone", IIF(Not Empty(@_Phone_Number), @_Phone_Number, 0000000000),  
        "Grade_Level__c", IIF(Not Empty(@_Grade_Level), @_Grade_Level, "Not Mentioned"),  
        "Title", IIF(Not Empty(@_Job_Title), @_Job_Title, 'Unknown'),
        "Job_Function__c",IIF(Not Empty(@sfJobFunction), @sfJobFunction, ''),
        "Country", IIF(Not Empty(@_Country_Name), @_Country_Name, "Australia"),  
        "State__c", IIF(Not Empty(@stateName),@stateName, ""),
        "Campaign_Name__c", IIF(Not Empty(@_UTM_Campaign_entry),@_UTM_Campaign_entry, ""),
        "Campaign_Code__c", IIF(Not Empty(@_Campaign_Code),@_Campaign_Code, ""),
        "State", IIF(Not Empty(@stateName),@stateName, ""),   
        "PostalCode", IIF(Not Empty(@_Postal_Code), @_Postal_Code, "0000"), 
        "Company", IIF(Not Empty(@_School_Name), @_School_Name, "NA Currently"),
        "No_of_licences__c",  IIF(Not Empty(@_No_Of_Licences), @_No_Of_Licences, 0), 
        "Marketing_Interest__c", IIF(@_Subscriber_Opt_In, "Promotions;Product Information;Newsletter", "Promotions;Product Information;"),
        "LeadSource", @LeadSource,
        "Status", @LeadStatus,
        "Enquiry_Type__c", @enquiry,
        'UTM_Source__c', IIF(Not Empty(@_UTM_Source), @_UTM_Source, @referrer),
        'UTM_Medium__c', IIF(Not Empty(@_UTM_Medium), @_UTM_Medium, 'Unknown'),
        'UTM_Campaign__c', IIF(Not Empty(@_UTM_Campaign), @_UTM_Campaign, 'Organic'),
        'UTM_Content__c', IIF(Not Empty(@_UTM_Content), @_UTM_Content, 'Unknown'),
        'UTM_Term__c', IIF(Not Empty(@_UTM_Term), @_UTM_Term, 'Unknown'),
        'Description', IIF(Not Empty(@Description), @Description, 'Unknown'),
        'GCLID__c', IIF(Not Empty(@gclid), @gclid, 'Unknown'),
        'Subject__c', IIF(Not Empty(@formattedSubjects), @formattedSubjects, ''),
        'Product_Interest__c',@_Product_Interest_c,
        'Existing_Customer__c',@Existing_Customer,
        'Territory__c', @Territory
    )
]%%
<script runat="server">
} catch (err) {
    var errorMessage = "Error Message: " + Stringify(err.message) + " " + Stringify(err.description);
    Write(errorMessage);
    
    Variable.SetValue("@errorMessage", errorMessage);
   
}
</script>
%%[
if not empty(@errorMessage) then
        set @SubscriberKey = "vijay.kapadam@partners.3plearning.com"
        set @EmailAddress  = "vijay.kapadam@partners.3plearning.com"


         SET @Language = "EN"
         SET @TriggerSendExternalKey = "243035"


         /* Specify the external key of the TriggerSend */
         SET @TriggerSend = CreateObject("TriggeredSend")
         SET @TriggerSendDefinition = CreateObject("TriggeredSendDefinition")
         SetObjectProperty(@TriggerSendDefinition, "CustomerKey", @TriggerSendExternalKey)
         SetObjectProperty(@TriggerSend, "TriggeredSendDefinition", @TriggerSendDefinition)

         /* Specify the email address and the subscriber key */
         SET @TriggerSendSubscriber = CreateObject("Subscriber")
         SetObjectProperty(@TriggerSendSubscriber, "EmailAddress", @EmailAddress) 
         SetObjectProperty(@TriggerSendSubscriber, "SubscriberKey", @SubscriberKey) 

         /* Fill out the Error field in the TriggerSend data extension */
         SET @TriggerSendError = CreateObject("Attribute")
         SetObjectProperty(@TriggerSendError, "Name", "Error_Message")
         SetObjectProperty(@TriggerSendError,"Value", @errorMessage)
         AddObjectArrayItem(@TriggerSend, "Attributes", @TriggerSendError)
         
         /* Fill out the Referrer field in the TriggerSend data extension */
         SET @TriggerSendReferrer = CreateObject("Attribute")
         SetObjectProperty(@TriggerSendReferrer, "Name", "Http_Referrer")
         SetObjectProperty(@TriggerSendReferrer,"Value", @http_referrer)
         AddObjectArrayItem(@TriggerSend, "Attributes", @TriggerSendReferrer)

         AddObjectArrayItem(@TriggerSend, "Subscribers", @TriggerSendSubscriber)  

         SET @TriggerSend_statusCode = InvokeCreate(@TriggerSend, @TriggerSend_statusMsg, @errorCode) 

         IF @TriggerSend_statusCode != "OK" THEN
          OUTPUTLINE(CONCAT("Status: ",@TriggerSend_statusMsg," / Code: ",@errorCode))
         ENDIF
endif
]%%
